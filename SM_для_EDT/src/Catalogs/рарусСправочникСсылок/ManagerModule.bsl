
// Получение одного значения из справочника ссылок
//------------------------------------------------

Функция ПолучитьЗначение(Папка,Элемент,ЗначениеПоУмолчанию=Неопределено) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Папка",Папка);
	Запрос.УстановитьПараметр("Элемент",Элемент);
	
	Запрос.Текст="
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спр.Значение
	|ИЗ
	|	Справочник.рарусСправочникСсылок КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|	И Спр.Код = &Элемент
	|	И НЕ Спр.ЭтоГруппа
	|	И Спр.Родитель.Код = &Папка
	|	И Спр.Родитель.ЭтоГруппа";
	
	Выборка=Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

// Получение массива значений из справочника ссылок
//-------------------------------------------------

Функция ПолучитьМассивЗначений(Папка1,Папка2) Экспорт
	
	Запрос=Новый Запрос;
	Запрос.УстановитьПараметр("Папка1",Папка1);
	Запрос.УстановитьПараметр("Папка2",Папка2);
	
	Запрос.Текст="
	|ВЫБРАТЬ
	|	Спр.Значение КАК Значение
	|ИЗ
	|	Справочник.рарусСправочникСсылок КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|	И НЕ Спр.ЭтоГруппа
	|	И Спр.Родитель.Код = &Папка2
	|	И Спр.Родитель.ЭтоГруппа
	|	И Спр.Родитель.Родитель.Код = &Папка1
	|	И Спр.Родитель.Родитель.ЭтоГруппа
	|
	|УПОРЯДОЧИТЬ ПО
	|	Значение";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Значение");
	
КонецФункции
