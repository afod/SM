
Функция СформироватьТабДок(Документ) Экспорт
	
	// Выводим шапку
	Макет = Отчеты.рарусТопливныйОтчет.ПолучитьМакет("РейсовыйОтчет");
	
	Таб = новый ТабличныйДокумент;
	Таб.Очистить();
	Таб.ОбластьПечати = Таб.Область(,1,,5);
	Таб.ЧерноБелаяПечать = Ложь;
	Таб.АвтоМасштаб = Истина;
	Таб.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	Таб.ОтображатьСетку = Ложь;
	Таб.ОтображатьЗаголовки = Ложь;
	Таб.ОтображатьГруппировки = Ложь;
	Таб.ТолькоПросмотр = Истина;
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Судно = Документ.Судно;
	Область.Параметры.НаименованиеОтчета = "Топливный отчет";
	Таб.Вывести(Область);
	// ++ rarus Камаев П.В. 02.10.2020 Задача № 24505
	Проект = Документ.Судно.Проект;
	// -- rarus Камаев П.В. 02.10.2020
	
	// Выводим строки топливного отчета
	// ++ rarus Чернавин Г.К 18.06.2020
	НомерДокумента = Документы.vftРейс.НомерБезПрефикса(Документ.Номер);
	Врем = Формат(НомерДокумента,"ЧН = ; ЧГ = 0") + " / " + Формат(Год(Документ.Дата),"ЧГ = 0");
	// -- rarus Чернавин Г.К 18.06.2020
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,1,,Врем,"Номер рейса / год");
	
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,2,,рарусОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеНаименованияРейса(Документ),"Порт выгрузки / порт погрузки / порт выгрузки");
	
	Врем = ?(Документ.Пункты.Количество() = 0,"",Формат(Документ.Пункты[0].Приход,"ДФ = 'dd.MM.yyyy / HH:mm'"));
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,3,,Врем,"Дата/время выхода из порта выгрузки");
	
	Врем = рарусОтчеты.ПолучитьПредставлениеТопливо(Документ, "Н");
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,4,,Врем,"Остаток на начало рейса: " + рарусОтчеты.ПолучитьПредставлениеТопливоШапка(Проект));
	
	// Груз
	//-----
	Для Каждого Стр из рарусОтчеты.ПолучитьПредставлениеГрузы(Документ) Цикл
		Если Стр.ЭтоПерваяСтрока Тогда
			рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,5,,Стр.Значение,Стр.Описание);
		Иначе
			рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,,Стр.Значение,Стр.Описание);
		КонецЕсли;
	КонецЦикла;
	
	// Значения времени по рейсу
	//--------------------------
	ВремяРейса = ПолучитьЗначенияВремениПоРейсуДляТопливногоОтчета(Документ);
	
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,6,"А",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.А,Истина),"Время рейса, сутки");
	
	Если Документ.Судно.Проект.Толкач Тогда
		ВремяРейса.Б = ВремяРейса.Б + ВремяРейса.З;
		ВремяРейса.В = ВремяРейса.В + ВремяРейса.П;
		ВремяРейса.Г = ВремяРейса.Г-ВремяРейса.З-ВремяРейса.П;
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Б",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Б,Истина) + " / " + 
		рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.З,Истина),
		"Ход с грузом, сутки (ч:мин) / в т.ч. маневры на переходе (З), сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"В",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.В,Истина) + " / " + 
		рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.П,Истина),
		// ++ rarus Камаев П.В. 08.10.2020 Задача № 23705
		//"Ход порожнем, сутки (ч:мин) / в т.ч. маневры, сутки");
		"Ход порожнем, сутки (ч:мин) / маневры, сутки");
		// -- rarus Камаев П.В. 08.10.2020
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Г",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Г,Истина),"Всего стоянки, сутки");
		
	ИначеЕсли Документ.Судно.ПринадлежитЭлементу(Справочники.vftСуда.НайтиПоНаименованию("RSD")) Тогда
		ВремяРейса.Б = ВремяРейса.Б + ВремяРейса.З;
		ВремяРейса.Г = ВремяРейса.Г-ВремяРейса.З;
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Б",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Б,Истина),"Ход с грузом, сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"В",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.В,Истина),"Ход порожнем, сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Г",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Г,Истина),"Всего стоянки, сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"З",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.З,Истина),"Время перехода с якорной стоянки до места выгрузки, сутки");
		
	Иначе
		ВремяРейса.Б = ВремяРейса.Б + ВремяРейса.З;
		ВремяРейса.Г = ВремяРейса.Г-ВремяРейса.З;
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Б",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Б,Истина),"Ход с грузом, сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"В",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.В,Истина),"Ход порожнем, сутки");
		рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет, ,"Г",рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗС_Формат(ВремяРейса.Г,Истина),"Всего стоянки, сутки");
	КонецЕсли;
	
	// Продолжаем выводить строки топливного отчета
	//---------------------------------------------
	Врем = ?(Документ.Пункты.Количество() = 0,"",Формат(Документ.Пункты[Документ.Пункты.Количество()-1].Отход,"ДФ = 'dd.MM.yyyy / HH:mm'"));
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,7,,Врем,"Дата/время окончания рейса");
	
	Врем = рарусОтчеты.ПолучитьПредставлениеТопливо(Документ, "Р");
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,8,,Врем,"Израсходовано топлива: " + рарусОтчеты.ПолучитьПредставлениеТопливоШапка(Проект));
	
	// Бункеровки
	//-----------
	Для Каждого Стр из рарусОтчеты.ПолучитьПредставлениеБункеровки(Документ) Цикл
		Если Стр.ЭтоПерваяСтрока Тогда
			рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб, Макет,9,, Стр.Значение, Стр.Описание);
		Иначе
			рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб, Макет,,, Стр.Значение, Стр.Описание);
		КонецЕсли;
	КонецЦикла;
	
	// Продолжаем выводить строки топливного отчета
	//---------------------------------------------
	Врем = рарусОтчеты.ПолучитьПредставлениеТопливо(Документ, "К");
	рарусОтчеты.ВывестиСтрокуРейсовогоОтчета(Таб,Макет,10,,Врем,"Остаток на конец рейса: " + рарусОтчеты.ПолучитьПредставлениеТопливоШапка(Проект));
	
	Таб.ВерхнийКолонтитул.НачальнаяСтраница = 1;
	Таб.ВерхнийКолонтитул.ТекстСправа = "Стр.[&НомерСтраницы] из [&СтраницВсего]";
	Таб.ВерхнийКолонтитул.Выводить = Истина;
	
	Таб.НижнийКолонтитул.НачальнаяСтраница = 1;
	Таб.НижнийКолонтитул.ТекстСправа = "Отчет сформирован [&Дата] [&Время]";
	Таб.НижнийКолонтитул.Выводить = Истина;
	
	Возврат Таб
	
КонецФункции

Функция ПолучитьЗначенияВремениПоРейсуДляТопливногоОтчета(Документ)
	
	//ПолучитьТЗТиповыхОперацийиАналитик()
	ТЗТиповыхОперацийиАналитик = рарусОтчеты.ПолучитьТЗТиповыхОперацийиАналитик();
	
	ВремяРейса = Новый Структура("А,Б,В,Г,З,П");
	Для Каждого Стр из ВремяРейса Цикл
		ВремяРейса[Стр.Ключ] = 0;
	КонецЦикла;
	
	// Получение общего времени по рейсу
	//----------------------------------
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",Документ.Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	ЕСТЬNULL(Данные.БукваА, 0) КАК БукваА,
	|	ЕСТЬNULL(Данные.БукваБ, 0) КАК БукваБ,
	|	ЕСТЬNULL(Данные.БукваВ, 0) КАК БукваВ,
	|	ЕСТЬNULL(Данные.БукваГ, 0) КАК БукваГ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(Пункты.ХодовоеВремя + Пункты.ВремяСтоянки) КАК БукваА,
	|		СУММА(ВЫБОР
	|				КОГДА Пункты.ГруженыйРейс
	|					ТОГДА Пункты.ХодовоеВремя + Пункты.ХодовоеВремяНаСтоянке
	|				ИНАЧЕ 0
	|			КОНЕЦ) КАК БукваБ,
	|		СУММА(ВЫБОР
	|				КОГДА Пункты.ГруженыйРейс
	|					ТОГДА 0
	|				ИНАЧЕ Пункты.ХодовоеВремя + Пункты.ХодовоеВремяНаСтоянке
	|			КОНЕЦ) КАК БукваВ,
	|		СУММА(Пункты.ВремяСтоянки - Пункты.ХодовоеВремяНаСтоянке) КАК БукваГ
	|	ИЗ
	|		Документ.vftРейс.Пункты КАК Пункты
	|	ГДЕ
	|		Пункты.Ссылка = &Ссылка) КАК Данные";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВремяРейса.А = Выборка.БукваА;
		ВремяРейса.Б = Выборка.БукваБ;
		ВремяРейса.В = Выборка.БукваВ;
		ВремяРейса.Г = Выборка.БукваГ;
	КонецЕсли;
	
	// Получение времени по конкретным причинам
	//-----------------------------------------
	Для Каждого Стр из Документ.Пункты Цикл
		Если Стр.ВремяСтоянки = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// К данным по прочим причинам стоянки добавляем данные реквизита "МаневрыВПункте", а также основную причину стоянки
		//------------------------------------------------------------------------------------------------------------------
		СписокПР = Документ.ПрочиеПричиныСтоянки.Выгрузить(Новый Структура("ИД",Стр.ИД),"ПричинаСтоянки, Аналитика, ВремяСтоянки");
		
		Если Стр.МаневрыВПункте<>0 Тогда  
			СтрПР = СписокПР.Добавить();
			СтрПР.ПричинаСтоянки	= Справочники.vftТиповыеОперации.Маневры;
			СтрПР.ВремяСтоянки		= Стр.МаневрыВПункте;
		КонецЕсли;
		
		СтрПР = СписокПР.Добавить();
		СтрПР.ПричинаСтоянки		= Стр.ПричинаСтоянки;
		СтрПР.ВремяСтоянки			= Стр.ВремяСтоянки - Стр.ХодовоеВремяНаСтоянке - СписокПР.Итог("ВремяСтоянки");
		
		СписокПР.Свернуть("ПричинаСтоянки, Аналитика","ВремяСтоянки");
		
		// Перебираем все причины стоянки
		//-------------------------------
		Для Каждого СтрПР из СписокПР Цикл
			Если рарусОтчеты.УсловиеВыполнено(ТЗТиповыхОперацийиАналитик, "З", СтрПР.ПричинаСтоянки, СтрПР.Аналитика) Тогда
				//СтрПР.ПричинаСтоянки = Справочники.vftТиповыеОперации.МаневрыНаПереходе Тогда
				ВремяРейса.З = ВремяРейса.З + СтрПР.ВремяСтоянки;
				
			// ++ rarus Камаев П.В. 08.10.2020 Задача № 23705
			ИначеЕсли //рарусОтчеты.УсловиеВыполнено(ТЗТиповыхОперацийиАналитик, "П", СтрПР.ПричинаСтоянки, СтрПР.Аналитика) Тогда
				СтрПР.ПричинаСтоянки = Справочники.vftТиповыеОперации.Маневры Тогда
			// -- rarus Камаев П.В. 08.10.2020
			ВремяРейса.П = ВремяРейса.П + СтрПР.ВремяСтоянки;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// ++ rarus Камаев П.В. 08.10.2020 Задача № 23705
		//++ rarus isaeva 24.03.2021
		//закомментировано вычитание времени маневров,
	//ВремяРейса.В = ВремяРейса.В - ВремяРейса.П;
		//-- rarus isaeva 24.03.2021
	// -- rarus Камаев П.В. 08.10.2020
	
	Возврат ВремяРейса;
	
КонецФункции

