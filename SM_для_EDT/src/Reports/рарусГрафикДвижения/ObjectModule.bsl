
Функция СформироватьТабДок(Документ) Экспорт
	
	ЭтоТолкач = Документ.Судно.Проект.Толкач;
	
	Если ЭтоТолкач Тогда
		Макет = ЭтотОбъект.ПолучитьМакет("УчетВремениБуксиры");
	Иначе
		Макет = ЭтотОбъект.ПолучитьМакет("УчетВремени");
	КонецЕсли;
		
	Таб = новый ТабличныйДокумент;
	Таб.Очистить();
	Таб.ОбластьПечати			= Неопределено;
	Таб.ЧерноБелаяПечать		= Ложь;
	Таб.АвтоМасштаб				= Истина;
	Таб.ОриентацияСтраницы		= ОриентацияСтраницы.Портрет;
	Таб.ОтображатьСетку			= Ложь;
	Таб.ОтображатьЗаголовки		= Ложь;
	Таб.ОтображатьГруппировки	= Истина;
	Таб.ТолькоПросмотр			= Истина;
	
	Область = Макет.ПолучитьОбласть("Шапка");
	//Область.Параметры.Номер				= Документ.Номер;
	// ++ rarus Чернавин Г.К 18.06.2020 № 22279
	Область.Параметры.Номер = Документы.vftРейс.НомерБезПрефикса(Документ.Номер);
	// -- rarus Чернавин Г.К 18.06.2020 
	Область.Параметры.Судно				= Документ.Судно;
	Область.Параметры.НаименованиеРейса	= рарусОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеНаименованияРейса(Документ);
	Область.Параметры.Дата				= Формат(Документ.Дата,"ДЛФ=DD");
	Таб.Вывести(Область);
	
	// Выводим строки
	//---------------
	Для Каждого СтрП Из Документ.Пункты Цикл
		Область=Макет.ПолучитьОбласть("Строка");
		Область.Параметры.Пункты							= рарусОбщегоНазначенияКлиентСервер.ПолучитьПредставлениеПункта(СтрП);
		Область.Параметры.ХодовоеВремя						= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(СтрП.ХодовоеВремя);
		Область.Параметры.ВремяСтоянки						= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(СтрП.ВремяСтоянки);
		Область.Параметры.ХодовоеВремяНаСтоянке				= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(СтрП.ХодовоеВремяНаСтоянке);
		Область.Параметры.ПредставлениеПрочихПричинСтоянки	= СтрП.ПредставлениеПрочихПричинСтоянки;
		Область.Параметры.ПричинаСтоянки					= ПолучитьПредставлениеПричиныСтоянки(СтрП);
		Область.Параметры.Приход							= СтрП.Приход;
		Область.Параметры.Отход								= СтрП.Отход;
		Область.Параметры.ВГрузу							= ?(СтрП.ГруженыйРейс,"Б","В");
		Область.Параметры.Примечание						= СтрП.Примечание;
		Область.Параметры.НомерСтрокиДокумента				= "НомерСтрокиДокумента_УВ_"+Формат(СтрП.НомерСтроки,"ЧН=; ЧГ=0");
		Если ЭтоТолкач Тогда
			Область.Параметры.БуксируемыеСуда				= ПолучитьПредставлениеСекции(Документ, СтрП.ИД);
		КонецЕсли;
		Таб.Вывести(Область);
	КонецЦикла;
	
	// Выводим итоги
	//--------------
	Врем = Документ.Пункты.Выгрузить(,"ГруженыйРейс,ХодовоеВремя,ВремяСтоянки,ХодовоеВремяНаСтоянке");
	Врем.Свернуть("ГруженыйРейс","ХодовоеВремя,ВремяСтоянки,ХодовоеВремяНаСтоянке");
	Врем.Сортировать("ГруженыйРейс");
	
	Для Каждого Стр из Врем Цикл
		Область = Макет.ПолучитьОбласть("Итого");
		Область.Параметры.Пункты				= ?(Стр.ГруженыйРейс,"Итого за груженый рейс","Итого за порожний рейс");
		Область.Параметры.ХодовоеВремя			= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Стр.ХодовоеВремя);
		Область.Параметры.ВремяСтоянки			= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Стр.ВремяСтоянки);
		Область.Параметры.ХодовоеВремяНаСтоянке	= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Стр.ХодовоеВремяНаСтоянке);
		Таб.Вывести(Область);
	КонецЦикла;
	
	Область=Макет.ПолучитьОбласть("Итого");
	Область.Параметры.Пункты					= "Итого за круговой рейс";
	Область.Параметры.ХодовоеВремя				= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Врем.Итог("ХодовоеВремя"));
	Область.Параметры.ВремяСтоянки				= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Врем.Итог("ВремяСтоянки"));
	Область.Параметры.ХодовоеВремяНаСтоянке		= рарусОбщегоНазначенияКлиентСервер.ПересчитатьВремя_ЗП_Формат(Врем.Итог("ХодовоеВремяНаСтоянке"));
	Таб.Вывести(Область);
	
	// Создадим группировку колонок "Примечание"
	//------------------------------------------
	Область = Таб.Область(,Таб.ШиринаТаблицы,,Таб.ШиринаТаблицы);
	Область.Сгруппировать("Примечание",РасположениеЗаголовкаГруппировкиТабличногоДокумента.Начало);
	
	Таб.ВерхнийКолонтитул.НачальнаяСтраница = 1;
	Таб.ВерхнийКолонтитул.ТекстСправа		= "Стр.[&НомерСтраницы] из [&СтраницВсего]";
	Таб.ВерхнийКолонтитул.Выводить			= Истина;
	
	Таб.НижнийКолонтитул.НачальнаяСтраница	= 1;
	Таб.НижнийКолонтитул.ТекстСправа		= "Отчет сформирован [&Дата] [&Время]";
	Таб.НижнийКолонтитул.Выводить			= Истина;
	
	Возврат Таб
	
КонецФункции

Функция ПолучитьПредставлениеПричиныСтоянки(СтрП)
	Перем Стр;
	
	Если НЕ ЗначениеЗаполнено(СтрП.ПричинаСтоянки) Тогда
		Стр="<Причина стоянки не выбрана>";
	Иначе
		Стр=""+СтрП.ПричинаСтоянки;
	КонецЕсли;
	
	// ++ rarus Чернавин Г.К 08.06.2020 № 22221
	Если ЗначениеЗаполнено(СтрП.Аналитика) Тогда
		Стр = Стр + ", " + СтрП.Аналитика;	
	КонецЕсли;
	// -- rarus Чернавин Г.К 08.06.2020
	
	Если не ПустаяСтрока(СтрП.ПредставлениеПричиныСтоянки) Тогда
		Стр=Стр+"."+Символы.ПС+СтрП.ПредставлениеПричиныСтоянки;
	КонецЕсли;
	
	Возврат Стр;
КонецФункции

Функция ПолучитьПредставлениеСекции(Документ, ИД)
	
	Результат="";
	Для Каждого Стр из Документ.Баржи.Выгрузить(Новый Структура("ИД",ИД),"БаржаПриход") Цикл
		Результат=Результат+?(ПустаяСтрока(Результат),"",", ")+Стр.БаржаПриход;
	КонецЦикла;
	Возврат Результат;
	
КонецФункции
