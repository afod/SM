#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Статус) Тогда
		Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОбластьСоздания) Тогда
		ОбластьСоздания = Перечисления.рарусОбластиРедактирования.Судно;	
	КонецЕсли;
	
	рарусСУБСервер.ОбработкаЗаполнениеДокументовСУБ(ЭтотОбъект);
					
КонецПроцедуры	
	
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ИСТИНА 
		|ИЗ
		|	Справочник.vftНесоответствия КАК vftНесоответствия
		|ГДЕ
		|	vftНесоответствия.ВладелецЗамечания = &ВладелецЗамечания
		|	И НЕ vftНесоответствия.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("ВладелецЗамечания", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			ТекстОшибки = НСтр("ru = 'По данному мероприятию уже созданы замечания';
			|en = 'Comments have already been created for this event.'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"",
			,
			Отказ);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Судно");
		МассивНепроверяемыхРеквизитов.Добавить("МестоПроведения");	
		МассивНепроверяемыхРеквизитов.Добавить("ДатаИнспекции");		
		МассивНепроверяемыхРеквизитов.Добавить("ФИОКапитана");	
		МассивНепроверяемыхРеквизитов.Добавить("ФиоИнспектора");		
	КонецЕсли;
		
	ТипМероприятия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидМероприятия, "ТипМероприятия");
	
	Если ТипМероприятия <> Неопределено Тогда
		Если ТипМероприятия = Перечисления.рарусТипыМероприятия.ОсвидетельствованиеСудна 
			Или ТипМероприятия = Перечисления.рарусТипыМероприятия.ВнешнийАудит
			Или ТипМероприятия = Перечисления.рарусТипыМероприятия.ОсвидетельствованиеКомпании Тогда 
			МассивНепроверяемыхРеквизитов.Добавить("ФиоИнспектора");					
		КонецЕсли;
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);	
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик;
	
	ИдентификаторСтрокиПланГрафика = " ";
	МероприятиеПоПлану = Ложь;
	ПредставлениеМероприятия = " ";
	
	// ++ rarus zalikh 2021-05-24 [27313]
	Анкета = Документы.Анкета.ПустаяСсылка();
	// -- rarus zalikh 2021-05-24 [27313]
	
КонецПроцедуры

#КонецОбласти
	
#КонецЕсли
