#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)

	//++ rarus isaeva 04.01.2020
	//Префикс =  Судно.КодБыстрогоВвода;
	Префикс = СформироватьПрефиксНомераДокумента();
	//-- rarus isaeva 04.01.2020

	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	vftОбщиеПроцедурыДокументовСервер.ПриКопировании(ЭтотОбъект,ОбъектКопирования);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		
		Возврат;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	 МассивНепроверяемыхРеквизитов = Новый Массив;
	
	 Если Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик Тогда
		 
		 МассивНепроверяемыхРеквизитов.Добавить("ФИОКапитана");
		 МассивНепроверяемыхРеквизитов.Добавить("ПериодИсполненияОбязанностейС");
		 МассивНепроверяемыхРеквизитов.Добавить("ПериодИсполненияОбязанностейПо");	 
		 	 
	 КонецЕсли;
	 
	 Если Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик
		 Или Статус = Перечисления.vftСтатусыДокументовСообщений.Отправлен Тогда
			 
		 МассивНепроверяемыхРеквизитов.Добавить("ОтветКомпании");
		 МассивНепроверяемыхРеквизитов.Добавить("Ответственный");
		 
	 КонецЕсли;
	 
	 Если Статус <> Перечисления.vftСтатусыДокументовСообщений.Черновик Тогда
		 
		 Для ТекИндекс = 0 По Предложения.Количество()-1 Цикл
			 
			 АдресОшибки = " " + НСтр("ru = 'в строке %НомерСтроки% списка.';
			 |en = 'in line %НомерСтроки% of the ""Goods"" list'");
			 АдресОшибки = СтрЗаменить(АдресОшибки, "%НомерСтроки%", Предложения[ТекИндекс].НомерСтроки);
			 
			 СтрокаТаблицы = Предложения[ТекИндекс];
			 
			 Если Не (СтрокаТаблицы.НетПредложения Или Не ПустаяСтрока(СтрокаТаблицы.Предложение)) Тогда
				 
				 ТекстОшибки = НСтр("ru = 'Не заполнено предложение по улучшению СУБ. Заполните или укажите, что предложений нет ';
				 |en = 'You must specify cancellation reason'");
				 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				 ТекстОшибки + АдресОшибки,
				 ЭтотОбъект,
				 ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Предложения", Предложения[ТекИндекс].НомерСтроки, "Предложение"),
				 ,
				 Отказ);	 
				 
			 КонецЕсли;
			 
		 КонецЦикла;
		 
	 КонецЕсли;
	 
	 ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);	
	 
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// ++ rarus ilshill 14.01.2020 № 26571
	
	// ++ Перенесена из обработчика приСозданииНаСервере
	Если Не ЗначениеЗаполнено(Ссылка) Тогда		
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	vftСУБ.Ссылка КАК Суб
		|ИЗ
		|	Справочник.vftСУБ КАК vftСУБ
		|ГДЕ
		|	vftСУБ.Архивный = ЛОЖЬ
		|	И vftСУБ.ПометкаУдаления = ЛОЖЬ
		|	И (vftСУБ.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.рарусТипыДокументацииСУБ.ДокументацияСУБ)
		|			ИЛИ vftСУБ.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.рарусТипыДокументацииСУБ.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	vftСУБ.НомерДокументаСУБ";
		
		Предложения.Загрузить(Запрос.Выполнить().Выгрузить())
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("рарусИспользоватьФункционалСУБ") Тогда	
		рарусСУБСервер.ОбработкаЗаполнениеДокументовСУБ(ЭтотОбъект);		
	КонецЕсли;

	// -- rarus ilshill 14.01.2020 № 26571
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьПрефиксНомераДокумента() Экспорт
	
	Префикс = "";
	
	Префикс = Префикс + ?(Судно <> Справочники.vftСуда.ПустаяСсылка(), Судно.КодБыстрогоВвода, "      ");
	Префикс = Префикс + "-";
	Префикс = Префикс + ?(Дата = Дата("00010101000000"), "  ", Прав(Формат(Дата, "ДФ=yyyy"), 2));
	Префикс = Префикс + "-";
	
	Возврат Префикс;
	
КонецФункции

#КонецОбласти

#КонецЕсли
