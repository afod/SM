///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Движения.ОтветыНаВопросыАнкет.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаСостав.Вопрос,
	|	ТаблицаСостав.ЭлементарныйВопрос,
	|	ТаблицаСостав.НомерЯчейки,
	|	ТаблицаСостав.Ответ,
	|	ТаблицаСостав.ОткрытыйОтвет,
	|	ТаблицаСостав.НомерСтроки
	|ПОМЕСТИТЬ Состав
	|ИЗ
	|	&ТаблицаСостав КАК ТаблицаСостав
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Состав.Вопрос,
	|	Состав.ЭлементарныйВопрос,
	|	Состав.НомерЯчейки,
	|	Состав.Ответ,
	|	Состав.ОткрытыйОтвет,
	|	ИСТИНА КАК Активность,
	|	&Ссылка КАК Регистратор,
	|	&Ссылка КАК Анкета,
	|	Состав.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Состав КАК Состав";
	
	Запрос.УстановитьПараметр("ТаблицаСостав",Состав);
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Движения.ОтветыНаВопросыАнкет.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
	КонецЕсли;
	
	Если РежимАнкетирования = Перечисления.РежимыАнкетирования.Интервью Тогда
		Интервьюер = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если РежимАнкетирования = Перечисления.РежимыАнкетирования.Интервью Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Опрос"));
	Иначе
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ШаблонАнкеты"));
	КонецЕсли;
	
	// ++ ilshil 09.07.2021	
	ПроверитьЗаполнениеКомментарияПриОтрицательномОтветеНаВопрос(Отказ);	
	// -- ilshil 09.07.2021

КонецПроцедуры

// ++ ilshil 09.07.2021
Процедура ПроверитьЗаполнениеКомментарияПриОтрицательномОтветеНаВопрос(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СоставАнкеты.Вопрос КАК Вопрос,
	|	СоставАнкеты.ЭлементарныйВопрос КАК ЭлементарныйВопрос,
	|	СоставАнкеты.НомерЯчейки КАК НомерЯчейки,
	|	СоставАнкеты.Ответ КАК Ответ,
	|	СоставАнкеты.ОткрытыйОтвет КАК ОткрытыйОтвет,
	|	СоставАнкеты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВтСостав
	|ИЗ
	|	&СоставАнкеты КАК СоставАнкеты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтСостав.НомерСтроки КАК НомерСтроки,
	|	ВопросыШаблонаАнкеты.Формулировка КАК Формулировка,
	|	ВопросыШаблонаАнкеты.Ссылка КАК Ссылка
	|ИЗ
	|	ВтСостав КАК ВтСостав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовАнкет КАК ВариантыОтветовАнкет
	|		ПО ВтСостав.Ответ = ВариантыОтветовАнкет.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВопросыШаблонаАнкеты КАК ВопросыШаблонаАнкеты
	|		ПО ВтСостав.Вопрос = ВопросыШаблонаАнкеты.Ссылка
	|ГДЕ
	|	ВариантыОтветовАнкет.РеквизитДопУпорядочивания = 2
	|	И ПОДСТРОКА(ВтСостав.ОткрытыйОтвет, 1, 200) = """"";
	
	Запрос.УстановитьПараметр("СоставАнкеты", Состав);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();	
		Пока Выборка.Следующий() Цикл					
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан комментарий для ответ на вопрос'") + "- "+ СтрЗаменить(Выборка.Ссылка.ПолныйКод(),"/",".") + " " + Выборка.Формулировка);	
			Отказ = Истина;			
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры
// -- ilshil 09.07.2021

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли