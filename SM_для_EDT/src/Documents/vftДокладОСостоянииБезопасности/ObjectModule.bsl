#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	//++ rarus isaeva 04.01.2020
	//Префикс =  Судно.КодБыстрогоВвода;
	Префикс = СформироватьПрефиксНомераДокумента();
	//-- rarus isaeva 04.01.2020
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	vftОбщиеПроцедурыДокументовСервер.ПриКопировании(ЭтотОбъект,ОбъектКопирования);
	
	Если ПолучитьФункциональнуюОпцию("рарусИспользоватьФункционалСУБ") Тогда
		
		Ответственный = " ";
		ОтветКомпании = " ";
		ПредоложенияПоКорректуреСудовойСУБ.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда	
		Возврат;	
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Судно) Тогда
		ПараметрыФункциональнойОпции = Новый Структура("Судно", Судно);
		ИспользоватьФункционалСУБ = ПолучитьФункциональнуюОпцию("рарусИспользоватьФункционалСУБ", ПараметрыФункциональнойОпции);
		Если (НЕ ИспользоватьФункционалСУБ) И (Статус = Перечисления.vftСтатусыДокументовСообщений.Отправлен) Тогда
			
			ЗаполнитьСоставСудовогоКомитета(ЭтотОбъект);
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ИспользоватьФункционалСУБ = ПолучитьФункциональнуюОпцию("рарусИспользоватьФункционалСУБ");
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик 
		Или ИспользоватьФункционалСУБ Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ФИОКапитана");
		МассивНепроверяемыхРеквизитов.Добавить("МесяцОбзора");
		МассивНепроверяемыхРеквизитов.Добавить("Председатель");
		МассивНепроверяемыхРеквизитов.Добавить("ОфицерПоБезопасности");
		МассивНепроверяемыхРеквизитов.Добавить("СПКМ");
		МассивНепроверяемыхРеквизитов.Добавить("СМХ");
		МассивНепроверяемыхРеквизитов.Добавить("ПредставительМоряков");
		МассивНепроверяемыхРеквизитов.Добавить("ПредставительРядовогоСостава");
		
	КонецЕсли;
	
	Если Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ТехническоеСостояниеСудна");
		МассивНепроверяемыхРеквизитов.Добавить("СостояниеПротивопожарногоОборудования");
		
	КонецЕсли;
	
	Если Не НесчастныеСлучаи 
		Или Статус = Перечисления.vftСтатусыДокументовСообщений.Черновик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВиднесчастныхСлучаев");
	КонецЕсли;
	
	Если Не ИспользоватьФункционалСУБ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СоставСудовогоКомитета");
		МассивНепроверяемыхРеквизитов.Добавить("ВиднесчастныхСлучаев");
	КонецЕсли;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);	
		
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("рарусИспользоватьФункционалСУБ") Тогда
		
		ИнициализироватьДокумент(ДанныеЗаполнения);
		
		рарусСУБСервер.ОбработкаЗаполнениеДокументовСУБ(ЭтотОбъект);

	КонецЕсли;
	
КонецПроцедуры

Функция ЗаполнитьСоставСудовогоКомитета(ДанныеДляЗаполнения) Экспорт
	
	ЕстьИзменения = Ложь;
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "Председатель", ДанныеДляЗаполнения.Председатель));
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "Офицер по безопасности", ДанныеДляЗаполнения.ОфицерПоБезопасности));
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "СПКМ", ДанныеДляЗаполнения.СПКМ));
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "СМХ", ДанныеДляЗаполнения.СМХ));
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "Представитель моряков", ДанныеДляЗаполнения.ПредставительМоряков));
	МассивРеквизитов.Добавить(Новый Структура("Роль, ФиоЧленаКомитета", "Представитель рядового состава", ДанныеДляЗаполнения.ПредставительРядовогоСостава));
	
	Для каждого СтруктураЗаполнения Из МассивРеквизитов Цикл
		
		ЕстьИзмененияВСтроке = ЗаполнитьЭлементСоставаСудовогоКомитета(СоставСудовогоКомитета, СтруктураЗаполнения);
		ЕстьИзменения = ЕстьИзменения ИЛИ ЕстьИзмененияВСтроке;
	
	КонецЦикла; 
	
	Возврат ЕстьИзменения;
	
КонецФункции // ЗаполнитьСоставСудовогоКомитета()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	СписокЗначенийРолей =  Документы.vftДокладОСостоянииБезопасности.СписокЗначенияРолей();
	
	Для Каждого Роль Из СписокЗначенийРолей Цикл
		
		НоваяСтрокаТаблицы = СоставСудовогоКомитета.Добавить();
		НоваяСтрокаТаблицы.Роль = Роль;
		НоваяСтрокаТаблицы.ФиоЧленаКомитета = " ";
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

Функция СформироватьПрефиксНомераДокумента() Экспорт
	
	Префикс = "";
	
	Префикс = Префикс + ?(Судно <> Справочники.vftСуда.ПустаяСсылка(), Судно.КодБыстрогоВвода, "      ");
	Префикс = Префикс + "-";
	Префикс = Префикс + ?(Дата = Дата("00010101000000"), "  ", Прав(Формат(Дата, "ДФ=yyyy"), 2));
	Префикс = Префикс + "-";
	
	Возврат Префикс;
	
КонецФункции

#КонецОбласти

Функция ЗаполнитьЭлементСоставаСудовогоКомитета(ТЧСоставСудовогоКомитета, СтруктураЗаполнения)
	
	ЕстьИзмененияВСтроке = Ложь;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Роль", СтруктураЗаполнения.Роль);
	
	НоваяСтрока = ТЧСоставСудовогоКомитета.НайтиСтроки(СтруктураОтбора);
	
	Если НоваяСтрока.Количество() > 0 Тогда
		
		НоваяСтрока = НоваяСтрока[0];
		Если СокрЛП(НоваяСтрока.ФиоЧленаКомитета) <> СокрЛП(СтруктураЗаполнения.ФиоЧленаКомитета) Тогда
			
			ЕстьИзмененияВСтроке = Истина;
			
		КонецЕсли; 
		
	Иначе
		
		Если ЗначениеЗаполнено(СтруктураЗаполнения.ФиоЧленаКомитета) Тогда
			
			НоваяСтрока = ТЧСоставСудовогоКомитета.Добавить();
			ЕстьИзмененияВСтроке = Истина;
			
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если ЕстьИзмененияВСтроке Тогда
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураЗаполнения);
		
	КонецЕсли; 
	
	Возврат ЕстьИзмененияВСтроке;
	
КонецФункции // ЗаполнитьЭлементСоставаСудовогоКомитета()

#КонецЕсли
