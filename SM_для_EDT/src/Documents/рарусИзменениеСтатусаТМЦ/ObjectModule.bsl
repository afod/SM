#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
	    И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		
		Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.рарусИнвентаризацияТМЦ") Тогда
			рарусИмущественныйУчетСервер.ЗаполнитьИзменениеСтатусаТМЦПоИнвентаризации(ЭтотОбъект, ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.впПриходТМЦ") Тогда
			рарусИмущественныйУчетСервер.ЗаполнитьИзменениеСтатусаТМЦПоПриходТМЦ(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;	
	
	Если ЭтоНовый() Тогда
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	// ++ rarus makole 2021-03-31
	СформированСМ = Истина;
	// -- rarus makole 2021-03-31
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	// ++ rarus makole 2021-03-31
	ПроверитьДоступностьРедактированияОбъекта(Отказ);
	// -- rarus makole 2021-03-31
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ИнформированиеОНедостаточномКоличествоНоменклатуры();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
		
КонецПроцедуры

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Если Не ЗначениеЗаполнено(Дата) тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = рарусОбщегоНазначенияСервер.ОсновнаяОрганизация();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Склад) тогда
		Склад = рарусОбщегоНазначенияСервер.СкладСудна();
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();	
	// ++ rarus makole 2021-03-31
	СформированСМ = Истина;
	// -- rarus makole 2021-03-31
	
КонецПроцедуры

Процедура ИнформированиеОНедостаточномКоличествоНоменклатуры()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Дата", Новый Граница(Дата, ВидГраницы.Исключая));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	рарусИзменениеСтатусаТМЦТовары.Номенклатура КАК Номенклатура,
	|	МИНИМУМ(рарусИзменениеСтатусаТМЦТовары.НомерСтроки) КАК НомерСтроки,
	|	СУММА(рарусИзменениеСтатусаТМЦТовары.КоличествоВыдано) КАК КоличествоКВыдаче
	|ПОМЕСТИТЬ ВТНоменклатураДокумента
	|ИЗ
	|	Документ.рарусИзменениеСтатусаТМЦ.Товары КАК рарусИзменениеСтатусаТМЦТовары
	|ГДЕ
	|	рарусИзменениеСтатусаТМЦТовары.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	рарусИзменениеСтатусаТМЦТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиИмущества.Номенклатура КАК Номенклатура,
	|	СУММА(ОстаткиИмущества.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	РегистрНакопления.рарусИмуществоНаСудах.Остатки(
	|			&Дата,
	|			Номенклатура В
	|					(ВЫБРАТЬ
	|						Т.Номенклатура
	|					ИЗ
	|						ВТНоменклатураДокумента КАК Т)
	|				И Склад = &Склад
	|				И Статус = ЗНАЧЕНИЕ(Перечисление.рарусСтатусыИмуществаНаСудне.НаСкладе)) КАК ОстаткиИмущества
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИмущества.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Списываемое.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(Списываемое.Номенклатура) КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(Списываемое.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
	|	Списываемое.НомерСтроки КАК НомерСтроки,
	|	Списываемое.КоличествоКВыдаче КАК КоличествоКВыдаче,
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	|ИЗ
	|	ВТНоменклатураДокумента КАК Списываемое
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатки КАК Остатки
	|		ПО Списываемое.Номенклатура = Остатки.Номенклатура
	|ГДЕ
	|	Списываемое.КоличествоКВыдаче > ЕСТЬNULL(Остатки.КоличествоОстаток, 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	УстановитьПривилегированныйРежим(Истина);
	НедостаточныеОстатки = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого Строка Из НедостаточныеОстатки Цикл
		ТекстСообщения = СтрШаблон("Недостаточно номенклатуры ""%1"" - к выдаче %2 %4, остаток на складе %3 %4",
			Строка.НоменклатураПредставление,
			Строка.КоличествоКВыдаче,
			Строка.КоличествоОстаток,
			Строка.ЕдиницаИзмеренияПредставление);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ДоступностьРедактированияОбъекта

Процедура ПроверитьДоступностьРедактированияОбъекта(Отказ)
	// ++ rarus makole 2021-03-31
	// После загрузки объекта из БМ ему устанавливается состояние ЗагруженИзБМ до окончания загрузки.
	// Затем статус меняется на Отправлен и редактирование созданных в БМ документов запрещается
	Отказ = Отказ ИЛИ НЕ (СформированСМ ИЛИ рарусИмущественныйУчетСервер.ЭтоЗагруженИзБМ(Ссылка));
	Если НЕ СформированСМ
		 И НЕ рарусИмущественныйУчетСервер.ЭтоЗагруженИзБМ(Ссылка) Тогда
		ОбщегоНазначения.СообщитьПользователю("Документ создан в береговом модуле, редактирование запрещено");
	КонецЕсли;
	// -- rarus makole 2021-03-31
КонецПроцедуры

#КонецОбласти

#КонецЕсли