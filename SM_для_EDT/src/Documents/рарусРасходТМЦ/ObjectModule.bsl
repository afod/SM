#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
	    И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		Если ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.рарусИнвентаризацияТМЦ") Тогда
			рарусИмущественныйУчетСервер.ЗаполнитьРасходТМЦПоИнвентаризации(ЭтотОбъект, ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.впПриходТМЦ") Тогда
			рарусИмущественныйУчетСервер.ЗаполнитьРасходТМЦПоПриходТМЦ(ЭтотОбъект, ДанныеЗаполнения);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.рарусИзменениеСтатусаТМЦ") Тогда
			рарусИмущественныйУчетСервер.ЗаполнитьРасходТМЦПоИзменениеСтатусаТМЦ(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;	
	
	Если ЭтоНовый() Тогда
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ПроверитьДоступностьРедактированияОбъекта(Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	// ++ rarus makole 2021-03-31
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	СформированСМ = Истина;
	// -- rarus makole 2021-03-31
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДоступностьРедактированияОбъекта

Процедура ПроверитьДоступностьРедактированияОбъекта(Отказ)
	// ++ rarus makole 2021-03-31
	// После загрузки объекта из БМ ему устанавливается состояние ЗагруженИзБМ до окончания загрузки.
	// Затем статус меняется на Отправлен и редактирование созданных в БМ документов запрещается
	Отказ = Отказ ИЛИ НЕ (СформированСМ ИЛИ рарусИмущественныйУчетСервер.ЭтоЗагруженИзБМ(Ссылка));
	Если НЕ СформированСМ
		 И НЕ рарусИмущественныйУчетСервер.ЭтоЗагруженИзБМ(Ссылка) Тогда
		ОбщегоНазначения.СообщитьПользователю("Документ создан в береговом модуле, редактирование запрещено");
	КонецЕсли;
	// -- rarus makole 2021-03-31
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеСлужебныхРеквизитов

Процедура ИнициализироватьДокумент(ДанныеЗаполнения)
	
	Если Не ЗначениеЗаполнено(Дата) тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Склад) тогда
		Склад = рарусОбщегоНазначенияСервер.СкладСудна();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация) тогда
		Организация = рарусОбщегоНазначенияСервер.ОсновнаяОрганизация();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидОперации) тогда
		ВидОперации = ПредопределенноеЗначение("Перечисление.рарусВидыОперацийРасходаТМЦ.СоСклада");
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	// ++ rarus makole 2021-03-31
	СформированСМ = Истина;
	// -- rarus makole 2021-03-31
	
КонецПроцедуры

#КонецОбласти

// См. описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
		
КонецПроцедуры


#КонецОбласти

#КонецЕсли