
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ТаблицаДляДвижений = Документы.впУчетНаработкиОборудования.ПодготовитьТаблицуДвижений(Ссылка);
	Движения.впНаработкаОбъектовРемонта.Записывать = Истина;
	
	Для Каждого СтрокаНаработкаОбъектов Из ТаблицаДляДвижений Цикл
		Движение = Движения.впНаработкаОбъектовРемонта.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, СтрокаНаработкаОбъектов);
		Движение.Период = Дата;
		Движение.Активность = Истина;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаНаработки из НаработкаОбъектов Цикл
		Если  СтрокаНаработки.РаспространятьНаПодчиненных Тогда
			ОчиститьПодчиненныеНаСервере(СтрокаНаработки.ОбъектРемонта);
			ЗаполнитьПодчиненныеНаСервере(СтрокаНаработки.ОбъектРемонта);
		Иначе 
			ОчиститьПодчиненныеНаСервере(СтрокаНаработки.ОбъектРемонта);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПодчиненныеНаСервере(ОбъектРемонта)
	ТаблицаЗаполнения = впОбщиеПроцедурыДокументовТОИР.ПолучитьТаблицуПодчиненныхОР(ОбъектРемонта);
	Для Каждого стрТаб из ТаблицаЗаполнения Цикл
		СтрокаПодч = ПодчиненныеОбъекты.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПодч, стрТаб);
	КонецЦикла;
КонецПроцедуры

Процедура ОчиститьПодчиненныеНаСервере(ОбъектРемонта)
	СтрокиДляУдаления = ПодчиненныеОбъекты.НайтиСтроки(Новый Структура("Объект", ОбъектРемонта));
	Для Каждого СтрокаДляУдаления из СтрокиДляУдаления Цикл
		ПодчиненныеОбъекты.Удалить(ПодчиненныеОбъекты.Индекс(СтрокаДляУдаления));
	КонецЦикла;
КонецПроцедуры

