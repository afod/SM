#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.Несоответствие"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.Замечание"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.Наблюдение"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.ЗначительноеНесоответствие"));	
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.Недостаток"));
	ДанныеВыбора.Добавить(ПредопределенноеЗначение("Перечисление.рарусТипыЗамечания.СерьезныйНедостаток"));	
	
	Если Параметры.Свойство("ВладелецЗамечания") Тогда	
		
		Если ТипЗнч(Параметры.ВладелецЗамечания) = Тип("ДокументСсылка.рарусМероприятиеСУБ") Тогда
			
			НовыйСписокТиповЗамечаний = ТипыЗамечанийАвтоПодборНаСервере(Параметры.ВладелецЗамечания);
			
			Если Не НовыйСписокТиповЗамечаний = Неопределено Тогда
				Если НовыйСписокТиповЗамечаний.Количество() > 0 Тогда			
					ДанныеВыбора.Очистить();
					ДанныеВыбора = НовыйСписокТиповЗамечаний;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТипыЗамечанийАвтоПодборНаСервере(ВладелецЗамечания)
	
	СписокТипов = Новый СписокЗначений;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	рарусМероприятиеСУБ.ВидМероприятия.Конвенция КАК ВидМероприятияКонвенция
	|ПОМЕСТИТЬ ОтборПоВиду
	|ИЗ
	|	Документ.рарусМероприятиеСУБ КАК рарусМероприятиеСУБ
	|ГДЕ
	|	рарусМероприятиеСУБ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	рарусКонвенцииДоступныеТипыЗамечаний.ТипЗамечания КАК ТипЗамечания
	|ИЗ
	|	Справочник.рарусКонвенции.ДоступныеТипыЗамечаний КАК рарусКонвенцииДоступныеТипыЗамечаний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПоВиду КАК ОтборПоВиду
	|		ПО рарусКонвенцииДоступныеТипыЗамечаний.Ссылка = ОтборПоВиду.ВидМероприятияКонвенция
	|ГДЕ
	|	рарусКонвенцииДоступныеТипыЗамечаний.ТипЗамечания <> ЗНАЧЕНИЕ(Перечисление.рарусТипыЗамечания.ПустаяССылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	рарусКонвенцииДоступныеТипыЗамечаний.ТипЗамечания";
	
	Запрос.УстановитьПараметр("Ссылка", ВладелецЗамечания);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда		
		СписокТипов = Неопределено;	
	Иначе
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокТипов.Добавить(Выборка.ТипЗамечания);		
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СписокТипов;
	
КонецФункции

#КонецОбласти	
	
#КонецЕсли
