// ++ rarus Чернавин Г.К 13.03.2020

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьСтрокиТЗ(ТЗ,СтруктураПоиска) Экспорт
		
	Для Каждого Стр из ТЗ.НайтиСтроки(СтруктураПоиска) Цикл
		ТЗ.Удалить(Стр);
	КонецЦикла;
	
КонецПроцедуры //УдалитьСтрокиТЗ

Функция СравнитьМассивы(Массив1,Массив2) Экспорт
	
	Если Массив1.Количество()<>Массив2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Эл из Массив1 Цикл
		Если Массив2.Найти(Эл)=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Эл из Массив2 Цикл
		Если Массив1.Найти(Эл)=Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции //СравнитьМассивы

Функция Между(А,ПорогН,ПорогВ) Экспорт
	Возврат не (А<ПорогН или А>ПорогВ);
КонецФункции //Между

#КонецОбласти

// -- rarus Чернавин Г.К 13.03.2020

// ++ rarus Камаев П.В. 23.03.2020 Задача № 20473

// Пересчет времени из количества секунд в сутки
функция ПересчитатьВремя_ЗС(Время) Экспорт
	
	Если ТипЗнч(Время)=Тип("Число") Тогда
		Возврат Окр(Время/86400,3);
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции //ПересчитатьВремя_ЗС

// Пересчет времени из количества секунд в сутки + форматирование
функция ПересчитатьВремя_ЗС_Формат(Время,ВыводитьПредставление) Экспорт
	
	Перем Суток,Результат;
	
	Суток=ПересчитатьВремя_ЗС(Время);
	
	Результат=Формат(Суток,"ЧН=; ЧГ=0");
	
	Если Суток>0 и ВыводитьПредставление Тогда
		Результат=Результат+"  ("+ПересчитатьВремя_ЗП_Формат(Время,0)+")";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции //ПересчитатьВремя_ЗС_Формат

// Пересчет времени из количество секунд в ЧЧ:ММ
//----------------------------------------------
Функция ПересчитатьВремя_ЗП(Время) Экспорт
	
	Если ТипЗнч(Время)=Тип("Число") Тогда
		Возврат Цел(Время/3600) + Цел(Время%3600/60)/100;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции //ПересчитатьВремя_ЗП

// Пересчет времени из количество секунд в ЧЧ:ММ + форматирование
//---------------------------------------------------------------
Функция ПересчитатьВремя_ЗП_Формат(Время,ВыводитьНоль=Неопределено) Экспорт
	
	Перем Врем;
	
	Врем=ПересчитатьВремя_ЗП(Время);
	
	Если ВыводитьНоль=0 Тогда
		Возврат Формат(Врем,"ЧДЦ=2; ЧРД=:; ЧН=; ЧГ=0");
	Иначе
		Возврат Формат(Врем,"ЧДЦ=2; ЧРД=:; ЧГ=0");
	КонецЕсли;
	
КонецФункции //ПересчитатьВремя_ЗП_Формат

Функция ПолучитьПредставлениеНаименованияРейса(Рейс) Экспорт
	
	Если Рейс.РаботаПоРаспоряжению Тогда
		Возврат ?(ПустаяСтрока(Рейс.НаименованиеРейса),"Работа по распоряжению диспетчера",СокрЛП(Рейс.НаименованиеРейса));
	Иначе
		// ++ rarus Камаев П.В. 06.04.2020 Задача № 20799
		мРейс = рарусОбщегоНазначенияКлиентСервер.ПолучитьМассивПортовРейса(Рейс.ПунктыСледования);
		//ПортОтправления
		ПортОтправления = "";
		Для каждого Элемент Из мРейс.ПортОтправления Цикл
			ПортОтправления = ПортОтправления + ?(ЗначениеЗаполнено(ПортОтправления), ", ", "") + Элемент;
		КонецЦикла;
		//ПортПогрузки
		ПортПогрузки = "";
		Для каждого Элемент Из мРейс.ПортПогрузки Цикл
			ПортПогрузки = ПортПогрузки + ?(ЗначениеЗаполнено(ПортПогрузки), ", ", "") + Элемент;
		КонецЦикла;
		//ПортОтправления
		ПортВыгрузки = "";
		Для каждого Элемент Из мРейс.ПортВыгрузки Цикл
			ПортВыгрузки = ПортВыгрузки + ?(ЗначениеЗаполнено(ПортВыгрузки), ", ", "") + Элемент;
		КонецЦикла;
		Возврат "" + ПортОтправления+ " / " + ПортПогрузки + " / " + ПортВыгрузки;
		//Возврат "" + Рейс.ПортОтправления + " / " + Рейс.ПортПогрузки + " / " + Рейс.ПортВыгрузки;
		// -- rarus Камаев П.В. 06.04.2020
	КонецЕсли;
	
КонецФункции //ПолучитьПредставлениеНаименованияРейса

Функция ПолучитьПредставлениеПункта(СтрП) Экспорт
	Перем Стр;
	
	Если Не ЗначениеЗаполнено(СтрП.Пункт) Тогда
		Стр = "<Пункт не выбран>";
	Иначе
		Стр = "" + СтрП.Пункт;
	КонецЕсли;
	
	Если не ПустаяСтрока(СтрП.ПунктКомментарий) Тогда
		Стр = Стр + Символы.ПС + "(" + СтрП.ПунктКомментарий + ")";
	КонецЕсли;
	
	Возврат Стр;
КонецФункции //ПолучитьПредставлениеПункта

// Функция возвращает наименьшее непустое значение
//------------------------------------------------
Функция Меньшее(Пар1,Пар2) Экспорт
	
	Если не ЗначениеЗаполнено(Пар1) Тогда
		Возврат Пар2;
	ИначеЕсли не ЗначениеЗаполнено(Пар2) Тогда
		Возврат Пар1;
	Иначе
		Возврат Мин(Пар1,Пар2);
	КонецЕсли;
	
КонецФункции //Меньшее

// Разница между датами в секундах
//--------------------------------
Функция РазницаДат(НачДата,КонДата) Экспорт
	
	Если ЗначениеЗаполнено(НачДата) и ЗначениеЗаполнено(КонДата) Тогда
		Возврат Макс(0,КонДата-НачДата);
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции //РазницаДат

// Функции получения описания типов
//---------------------------------
Функция ПолучитьОписаниеТиповСтроки(ДлинаСтроки) Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	КвалификаторСтроки = Новый КвалификаторыСтроки(ДлинаСтроки, ДопустимаяДлина.Переменная);
	Возврат Новый ОписаниеТипов(Массив, , КвалификаторСтроки);
КонецФункции //ПолучитьОписаниеТиповСтроки

Функция ПолучитьОписаниеТиповЧисла(Разрядность,РазрядностьДробнойЧасти=0) Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	КвалификаторЧисла = Новый КвалификаторыЧисла(Разрядность,РазрядностьДробнойЧасти);
	Возврат Новый ОписаниеТипов(Массив, КвалификаторЧисла);
КонецФункции //ПолучитьОписаниеТиповЧисла

Функция ПолучитьОписаниеТиповДаты(ЧастиДаты) Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Тип("Дата"));
	КвалификаторДаты = Новый КвалификаторыДаты(ЧастиДаты);
	Возврат Новый ОписаниеТипов(Массив, , , КвалификаторДаты);
КонецФункции //ПолучитьОписаниеТиповДаты

// -- rarus Камаев П.В. 23.03.2020

// ++ rarus Камаев П.В. 06.04.2020 Задача № 20799
// Возвращает структуру с ключами "ПортОтправления", "ПортПогрузки", "ПортВыгрузки"
Функция ПолучитьПортыРейса(ТЧ) Экспорт
	
	ПортОтправления	= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	ПортПогрузки	= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	ПортВыгрузки	= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	
	Для каждого Строка Из ТЧ Цикл
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортОтправления") Тогда
			ПортОтправления = Строка.Пункт;
		КонецЕсли;
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортПогрузки") Тогда
			ПортПогрузки = Строка.Пункт;
		КонецЕсли;
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортВыгрузки") Тогда
			ПортВыгрузки = Строка.Пункт;
		КонецЕсли;
	КонецЦикла;
	
	Структура = новый Структура;
	Структура.Вставить("ПортОтправления", ПортОтправления);
	Структура.Вставить("ПортПогрузки"	, ПортПогрузки);
	Структура.Вставить("ПортВыгрузки"	, ПортВыгрузки);
	
	Возврат Структура
	
КонецФункции //ПолучитьПортыРейса

// Возвращает структуру массивов с ключами "ПортОтправления", "ПортПогрузки", "ПортВыгрузки"
Функция ПолучитьМассивПортовРейса(ТЧ) Экспорт
	
	ПортОтправления	= новый Массив;
	ПортПогрузки	= новый Массив;
	ПортВыгрузки	= новый Массив;
	
	Для каждого Строка Из ТЧ Цикл
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортОтправления") Тогда
			ПортОтправления.Добавить(Строка.Пункт);
		КонецЕсли;
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортПогрузки") Тогда
			ПортПогрузки.Добавить(Строка.Пункт);
		КонецЕсли;
		Если Строка.ТипПункта = ПредопределенноеЗначение("Перечисление.рарусТипыПунктов.ПортВыгрузки") Тогда
			ПортВыгрузки.Добавить(Строка.Пункт);
		КонецЕсли;
	КонецЦикла;
	
	Структура = новый Структура;
	Структура.Вставить("ПортОтправления", ПортОтправления);
	Структура.Вставить("ПортПогрузки"	, ПортПогрузки);
	Структура.Вставить("ПортВыгрузки"	, ПортВыгрузки);
	
	Возврат Структура
	
КонецФункции //ПолучитьПортыРейса

// -- rarus Камаев П.В. 06.04.2020

