
#Область ОтчетОРейсообороте

//Пересчитывает ячейки в табличном документе
Процедура ПересчетЯчеек(ТабДок) Экспорт
	
	МассивМета = рарусОтчеты.ПолучитьМассивРеквизитов("Документы", "vftРейс");
	
	// Пересчет ячеек отчета
	Обл = ТабДок.Области;
	
	// Заполнение часов и секунд
	Для Каждого Эл из МассивМета Цикл
		Если Лев(Эл, 9) = "ОР_Время_" Тогда
			Рек10 = Эл;
			Рек11 = Лев(Эл, СтрДлина(Эл) - 1) + "1";
			Рек12 = Лев(Эл, СтрДлина(Эл) - 1) + "2";
			
			Обл[Рек11].Значение = Обл[Рек10].Значение;
			Обл[Рек12].Значение = Обл[Рек10].Значение;
		КонецЕсли;
	КонецЦикла;
	
	// Расчет пунктов 8-9, с учетом пункта 4
	П8 = рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_01_10.Значение, Обл.ОР_Время_02_10.Значение);
	П9 = рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_03_10.Значение, Обл.ОР_Время_05_10.Значение);
	
	П4 = рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_04АН_10.Значение, Обл.ОР_Время_04АК_10.Значение);
	Если П4 > 0 Тогда
		Если Обл.ОР_Время_04АН_10.Значение <= Обл.ОР_Время_02_10.Значение Тогда
			П8 = П8 - П4;
		ИначеЕсли Обл.ОР_Время_04АН_10.Значение >= Обл.ОР_Время_03_10.Значение Тогда
			П9 = П9 - П4;
		КонецЕсли;
	КонецЕсли;
	
	П4 = рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_04БН_10.Значение, Обл.ОР_Время_04БК_10.Значение);
	Если П4 > 0 Тогда
		Если Обл.ОР_Время_04БН_10.Значение <= Обл.ОР_Время_02_10.Значение Тогда
			П8 = П8 - П4;
		ИначеЕсли Обл.ОР_Время_04БН_10.Значение >= Обл.ОР_Время_03_10.Значение Тогда
			П9 = П9 - П4;
		КонецЕсли;
	КонецЕсли;
	
	П4 = рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_04ВН_10.Значение, Обл.ОР_Время_04ВК_10.Значение);
	Если П4 > 0 Тогда
		Если Обл.ОР_Время_04ВН_10.Значение <= Обл.ОР_Время_02_10.Значение Тогда
			П8 = П8 - П4;
		ИначеЕсли Обл.ОР_Время_04ВН_10.Значение >= Обл.ОР_Время_03_10.Значение Тогда
			П9 = П9 - П4;
		КонецЕсли;
	КонецЕсли;
	
	// Заполнение пунктов 8-12
	Обл.ОР_Расчет_08Ф.Значение = Окр(П8 / 3600, 1);
	Обл.ОР_Расчет_09Ф.Значение = Окр(П9 / 3600, 1);
	Обл.ОР_Расчет_10Ф.Значение = Окр(рарусОбщегоНазначенияКлиентСервер.РазницаДат(Обл.ОР_Время_05_10.Значение, Обл.ОР_Время_06_10.Значение) / 3600, 1);
	Обл.ОР_Расчет_11Ф.Значение = Обл.ОР_Расчет_08Ф.Значение + Обл.ОР_Расчет_09Ф.Значение;
	Обл.ОР_Расчет_11В.Значение = Обл.ОР_Расчет_11З.Значение - Обл.ОР_Расчет_11Ф.Значение;
	Обл.ОР_Расчет_12В.Значение = Обл.ОР_Расчет_12Ф.Значение - Обл.ОР_Расчет_12З.Значение;
	
КонецПроцедуры

//Заполняет данные из табличного документа в объект
Процедура ЗаполнитьДанныеДокумента(Объект, ТабДок) Экспорт
	
	Если ТабДок.Области.Найти("НомерОтчета") = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Модифицированность=Истина;
	
	ПустаяДата = Дата(1,1,1);
	
	// Заполняем порты
	// ++ rarus Камаев П.В. 06.04.2020 Задача № 20799
	//Объект.ОР_Порт_ПН = Объект.ПортОтправления;
	//Объект.ОР_Порт_ПК = Объект.ПортПогрузки;
	//Объект.ОР_Порт_ГН = Объект.ПортПогрузки;
	//Объект.ОР_Порт_ГК = Объект.ПортВыгрузки;
	мОбъект = рарусОбщегоНазначенияКлиентСервер.ПолучитьПортыРейса(Объект.ПунктыСледования);
	Объект.ОР_Порт_ПН = мОбъект.ПортОтправления;
	Объект.ОР_Порт_ПК = мОбъект.ПортПогрузки;
	Объект.ОР_Порт_ГН = мОбъект.ПортПогрузки;
	Объект.ОР_Порт_ГК = мОбъект.ПортВыгрузки;
	// -- rarus Камаев П.В. 06.04.2020
	
	Объект.ОР_Время_01_10 = рарусОтчеты.ПолучитьКонДатуОформленияДокументов(Объект.Дата, Объект.Судно);
	
	// Строки 02-06, 12
	ОР_Время_02_10 		= ПустаяДата;
	ОР_Время_03_10 		= ПустаяДата;
	ОР_Время_04АН_10 	= ПустаяДата;
	ОР_Время_04БН_10 	= ПустаяДата;
	ОР_Время_04ВН_10 	= ПустаяДата;
	ОР_Время_04АК_10 	= ПустаяДата;
	ОР_Время_04БК_10 	= ПустаяДата;
	ОР_Время_04ВК_10 	= ПустаяДата;
	ОР_Время_05_10		= ПустаяДата;
	ОР_Время_06_10		= ПустаяДата;
	
	ОР_Расчет_12Ф		= 0;
	
	ОР_Пункт_04А 		= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	ОР_Пункт_04Б 		= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	ОР_Пункт_04В 		= ПредопределенноеЗначение("Справочник.vftПунктыСледования.ПустаяСсылка");
	
	ФлагВыгрузкиЕщеНет = Истина;
	
	Для Каждого СтрП из Объект.Пункты Цикл
		Если СтрП.ПричинаСтоянки = ПредопределенноеЗначение("Справочник.vftТиповыеОперации.Погрузка") Тогда
			
			Для Каждого СтрБ из Объект.Баржи.НайтиСтроки(Новый Структура("ИД",СтрП.ИД)) Цикл
				ОР_Время_02_10	= рарусОбщегоНазначенияКлиентСервер.Меньшее(ОР_Время_02_10,СтрБ.ДатаПодачиНотиса);
				ОР_Время_03_10	= Макс(ОР_Время_03_10,СтрБ.ДокументыНаБортуКонДата);
				ОР_Расчет_12Ф	= ОР_Расчет_12Ф + СтрБ.ВесГруза;
			КонецЦикла;
			
		ИначеЕсли СтрП.ПричинаСтоянки = ПредопределенноеЗначение("Справочник.vftТиповыеОперации.Выгрузка") Тогда
			
			ФлагВыгрузкиЕщеНет = Ложь;
			Для Каждого СтрБ из Объект.Баржи.НайтиСтроки(Новый Структура("ИД",СтрП.ИД)) Цикл
				ОР_Время_05_10 = рарусОбщегоНазначенияКлиентСервер.Меньшее(ОР_Время_05_10,СтрБ.ДатаПодачиНотиса);
				ОР_Время_06_10 = Макс(ОР_Время_06_10,СтрБ.ДокументыНаБортуКонДата);
			КонецЦикла;
			
		ИначеЕсли ФлагВыгрузкиЕщеНет И 
			(СтрП.ПричинаСтоянки = ПредопределенноеЗначение("Справочник.vftТиповыеОперации.ПаузкаВыгрузка") 
			ИЛИ СтрП.ПричинаСтоянки = ПредопределенноеЗначение("Справочник.vftТиповыеОперации.ПаузкаДогрузка")) Тогда
			
			Если НЕ ЗначениеЗаполнено(ОР_Пункт_04А) Тогда
				ОР_Пункт_04А		= СтрП.Пункт;
				ОР_Время_04АН_10	= СтрП.Приход;
				ОР_Время_04АК_10	= СтрП.Отход;
			ИначеЕсли НЕ ЗначениеЗаполнено(ОР_Пункт_04Б) Тогда
				ОР_Пункт_04Б		= СтрП.Пункт;
				ОР_Время_04БН_10	= СтрП.Приход;
				ОР_Время_04БК_10	= СтрП.Отход;
			ИначеЕсли НЕ ЗначениеЗаполнено(ОР_Пункт_04В) Тогда
				ОР_Пункт_04В		= СтрП.Пункт;
				ОР_Время_04ВН_10	= СтрП.Приход;
				ОР_Время_04ВК_10	= СтрП.Отход;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Объект.ОР_Время_02_10	= ОР_Время_02_10;
	Объект.ОР_Время_03_10	= ОР_Время_03_10;
	Объект.ОР_Время_04АН_10	= ОР_Время_04АН_10;
	Объект.ОР_Время_04БН_10	= ОР_Время_04БН_10;
	Объект.ОР_Время_04ВН_10	= ОР_Время_04ВН_10;
	Объект.ОР_Время_04АК_10	= ОР_Время_04АК_10;
	Объект.ОР_Время_04БК_10	= ОР_Время_04БК_10;
	Объект.ОР_Время_04ВК_10	= ОР_Время_04ВК_10;
	Объект.ОР_Время_05_10	= ОР_Время_05_10;
	Объект.ОР_Время_06_10	= ОР_Время_06_10;
	
	Объект.ОР_Расчет_12Ф	= ОР_Расчет_12Ф;
	
	Объект.ОР_Пункт_04А		= ОР_Пункт_04А;
	Объект.ОР_Пункт_04Б		= ОР_Пункт_04Б;
	Объект.ОР_Пункт_04В		= ОР_Пункт_04В;
	
	// Строка 07
	Врем = Объект.Пункты.Количество();
	Если Врем = 0 Тогда
		ОР_Время_07_10 = ПустаяДата;
	Иначе
		ОР_Время_07_10 = Объект.Пункты[Врем - 1].Отход;
	КонецЕсли;
	Объект.ОР_Время_07_10	= ОР_Время_07_10;
	
	// Строки 13, 14
	СтрБ = Объект.Бункеровки.НайтиСтроки(новый Структура("ДатаБункеровки", Дата("39991215")));
	Если СтрБ.Количество() = 0 Тогда
		ОР_Расчет_13Ф = 0;
		ОР_Расчет_14Ф = 0;
	Иначе
		// ++ rarus Камаев П.В. 28.08.2020 Задача № 23501
		ОР_Расчет_13Ф = 0;
		ОР_Расчет_14Ф = 0;
		ТопливоМТ01 = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.ТопливоМТ01"), "Значение");
		ТопливоМТ05 = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.ТопливоМТ05"), "Значение");
		ТопливоМТ15 = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.ТопливоМТ15"), "Значение");
		ТопливоСМТ = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.ТопливоСМТ"), "Значение");
		
		МаслоГД = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.МаслоГД"), "Значение");
		МаслоДГ = рарусОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение("Справочник.рарусСправочникСсылок.МаслоДГ"), "Значение");
		Для каждого мСтр Из СтрБ Цикл
			Если ТопливоМТ01 = мСтр.ВидТоплива ИЛИ ТопливоМТ05 = мСтр.ВидТоплива
				ИЛИ ТопливоМТ15 = мСтр.ВидТоплива ИЛИ ТопливоСМТ = мСтр.ВидТоплива Тогда
				ОР_Расчет_13Ф = ОР_Расчет_13Ф + мСтр.Количество;
			КонецЕсли;
			Если МаслоГД = мСтр.ВидТоплива ИЛИ МаслоДГ = мСтр.ВидТоплива Тогда
				ОР_Расчет_14Ф = ОР_Расчет_14Ф + мСтр.Количество;
			КонецЕсли;
			
			//ОР_Расчет_13Ф = СтрБ[0].МазутКол + СтрБ[0].ДизтопливоКол;
			//ОР_Расчет_14Ф = СтрБ[0].МаслоГДКол + СтрБ[0].МаслоДГКол;
		КонецЦикла;
		// -- rarus Камаев П.В. 28.08.2020
		
	КонецЕсли;
	Объект.ОР_Расчет_13Ф	= ОР_Расчет_13Ф;
	Объект.ОР_Расчет_14Ф	= ОР_Расчет_14Ф;
	
КонецПроцедуры

#КонецОбласти