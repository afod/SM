
#Область ПрограммныйИнтерфейс

Функция СформироватьПечатнуюФормуЗаявкиНаСудно(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_впЗаявкаНаСнабжение";
	
	ДанныеДляПечати = ПолучитьДанныеДляПечатиЗаявки(МассивОбъектов);
	
	ДанныеПечати = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ДанныеТаблицы = ДанныеДляПечати.РезультатПоТаблице.Выгрузить();
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл				
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
			
		Таблица = ДанныеТаблицы.НайтиСтроки(СтруктураПоиска);
							
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Макет = Новый ТабличныйДокумент;
		ЗаполнитьРеквизитыШапки(ДанныеПечати, Макет, ТабличныйДокумент); 
				
		ЗаполнитьРеквизитыТаблицы(Таблица, Макет, ТабличныйДокумент);
		
		ЗаполнитьРеквизитыПодвала(ДанныеПечати, Макет, ТабличныйДокумент);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

#КонецОбласти

Процедура ЗаполнитьРеквизитыШапки(ДанныеПечати, Макет, ТабличныйДокумент)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.впЗаявкаНаСнабжение.ПФ_MXL_СудоваяЗаявка");
		
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Заполнить(ДанныеПечати);
	
	Область.Параметры.Заголовок = СтрШаблон("Заявка № %1 от %2", ДанныеПечати.Номер, Формат(ДанныеПечати.Дата,"ДФ=dd.MM.yyyy"));
	
	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры	

Процедура ЗаполнитьРеквизитыТаблицы(Талица, Макет, ТабличныйДокумент)		
		
	Область = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(Область);
	
	Для Каждого СтрокаТаблицы Из Талица цикл
		
		Область = Макет.ПолучитьОбласть("СтрокаТаблицы");

		СтруктураДанныхСтроки = СтруктураДанныхСтроки();				
		
		ЗаполнитьЗначенияСвойств(СтруктураДанныхСтроки, СтрокаТаблицы);
		
		СтруктураДанныхСтроки.Наименование = СтрокаТаблицы.Производитель;
		СтруктураДанныхСтроки.Название = СтрокаТаблицы.Номенклатура;
		
		Область.Параметры.Заполнить(СтруктураДанныхСтроки);				
		
		ТабличныйДокумент.Вывести(Область);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтруктураДанныхСтроки()
	
	Перем СтруктураДанныхСтроки;
	
	СтруктураДанныхСтроки = Новый Структура;
	СтруктураДанныхСтроки.Вставить("пп");
	СтруктураДанныхСтроки.Вставить("Наименование");
	СтруктураДанныхСтроки.Вставить("ЕдИзм");
	СтруктураДанныхСтроки.Вставить("Количество");
	СтруктураДанныхСтроки.Вставить("ЗаказаноеКоличество");
	СтруктураДанныхСтроки.Вставить("Комментарий");
	СтруктураДанныхСтроки.Вставить("Название");
	
	Возврат СтруктураДанныхСтроки;

КонецФункции

Процедура ЗаполнитьРеквизитыПодвала(ДанныеПечати, Макет, ТабличныйДокумент)		
		
	Область = Макет.ПолучитьОбласть("Подвал");
		
	ТабличныйДокумент.Вывести(Область);
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатиЗаявки(МассивОбъектов)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	впЗаявкаНаСнабжение.Ссылка КАК Ссылка,
	|	впЗаявкаНаСнабжение.Подразделение.Наименование КАК НазваниеСудна,
	|	впЗаявкаНаСнабжение.Номер КАК Номер,
	|	впЗаявкаНаСнабжение.Дата КАК Дата,
	|	впЗаявкаНаСнабжение.НомерСудовойЗаявки КАК НомерСудовойЗаявки,
	|	впЗаявкаНаСнабжение.ДатаРегистрацииСудовойЗаявки КАК ДатаРегистрацииСудовойЗаявки
	|ИЗ
	|	Документ.впЗаявкаНаСнабжение КАК впЗаявкаНаСнабжение
	|ГДЕ
	|	впЗаявкаНаСнабжение.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.Ссылка КАК Ссылка,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.Номенклатура КАК Номенклатура,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.Примечание КАК Комментарий,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.КоличествоУпаковок КАК КоличествоВЗипе,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.КоличествоУпаковок КАК ЗаказаноеКоличество,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.Номенклатура.ЕдиницаИзмерения КАК ЕдИзм,
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.НомерСтроки КАК пп,
	|	НоменклатураДополнительныеРеквизиты.Значение КАК Производитель
	|ИЗ
	|	Документ.впЗаявкаНаСнабжение.МатериалыИРаботы КАК впЗаявкаНаСнабжениеМатериалыИРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|		ПО впЗаявкаНаСнабжениеМатериалыИРаботы.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка
	|			И (НоменклатураДополнительныеРеквизиты.Свойство.Наименование = ""Производитель"")
	|ГДЕ
	|	впЗаявкаНаСнабжениеМатериалыИРаботы.Ссылка В(&МассивОбъектов)
	|	И НЕ впЗаявкаНаСнабжениеМатериалыИРаботы.Отменено
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	пп";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	ПакетРезультатовЗапроса = Запрос.ВыполнитьПакет();

	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("РезультатПоШапке", ПакетРезультатовЗапроса[0]);
	ДанныеПечати.Вставить("РезультатПоТаблице", ПакетРезультатовЗапроса[1]);
	
	Возврат ДанныеПечати;
	
КонецФункции	