Функция ПланОбменаПолныйОбъектДоступенДляРедактирования(Ссылка) Экспорт
	
	ПланОбменаПолный = ПланОбменаПолный();
	
	Возврат ЕстьРегистрацияНаПланеОбменаИВыгрузкаНеПроизводилась(Ссылка, ПланОбменаПолный);

КонецФункции 

Функция ЕстьРегистрацияНаПланеОбменаИВыгрузкаНеПроизводилась(Ссылка, ПланОбмена)
	
	Выборка = ПолучитьДанныеРегистрацииПоСсылке(Ссылка, ПланОбмена);
	
	Если Выборка.Следующий() тогда
		
		Возврат ПустойНомерСообщения(Выборка.НомерСообщения);
		
	КонецЕсли;			
	
	Возврат Ложь;

КонецФункции

Функция ЕстьРегистрацияНаПланеОбмена(Ссылка, ПланОбмена)
	
	Выборка = ПолучитьДанныеРегистрацииПоСсылке(Ссылка, ПланОбмена);

	ЕстьРегистрация =  Выборка.Количество() > 0;
	
	Возврат ЕстьРегистрация;

КонецФункции

Функция ПолучитьДанныеРегистрацииПоСсылке(Ссылка, ПланОбмена)
		
	Узлы = ПолучитьУзлыПланаОбмена(ПланОбмена);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", Узлы);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т.Ссылка КАК Ссылка,
	|	Т.НомерСообщения КАК НомерСообщения
	|ИЗ
	|	%Таблица%.Изменения КАК Т
	|ГДЕ
	|	Т.Узел В (&Узел)
	|	И Т.Ссылка = &Ссылка";
	
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Ссылка);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Таблица%", ИмяТаблицы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;

КонецФункции

Функция ПолучитьУзлыПланаОбмена(ПланОбмена)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Полный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.%ПланОбменаИмя% КАК Полный
	|ГДЕ
	|	Полный.ЭтотУзел = ЛОЖЬ";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПланОбменаИмя%", ПланОбмена);
	Узлы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат Узлы;

КонецФункции

Функция ПустойНомерСообщения(НомерСообщения)
	
	Возврат Не ЗначениеЗаполнено(НомерСообщения);

КонецФункции

Функция ПланОбменаПолный()

	Возврат "Полный";	

КонецФункции 

 