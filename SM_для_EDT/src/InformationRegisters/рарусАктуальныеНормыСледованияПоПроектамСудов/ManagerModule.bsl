// ++ rarus Камаев П.В. 03.04.2020 Задача № 20783
&НаСервере
Функция ПолучитьНормыСледования(ТекущийПункт, ПредыдущийПункт, ПроектСудна, ГрузБалласт, ОсновноеСудно) Экспорт
	
	СтруктураВозврата = Новый Структура("Время, Расстояние", 0, 0);
	
	Если ТекущийПункт = ПредыдущийПункт
		ИЛИ НЕ рарусОбщегоНазначенияВызовСервера.ЭтоСудноННФ(ОсновноеСудно) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПункт",ТекущийПункт);
	Запрос.УстановитьПараметр("ПредыдущийПункт",ПредыдущийПункт);
	Запрос.УстановитьПараметр("ПроектСудна",ПроектСудна);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("ОсновноеСудно", ОсновноеСудно);
	Запрос.УстановитьПараметр("КлассСудна", ОсновноеСудно.СимволКласса);
	
	ПустойПроект = новый Массив;
	ПустойПроект.Добавить(Неопределено);
	ПустойПроект.Добавить(Справочники.vftПроектыСудов.ПустаяСсылка());
	ПустойПроект.Добавить(Справочники.рарусТипыСоставов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойПроект",ПустойПроект);
	Запрос.УстановитьПараметр("ПустоеСудно",Справочники.vftСуда.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойКлассСудна",Справочники.рарусКлассыСудов.ПустаяСсылка());
	
	//1 Проект судна
	//2 Судно
	//3 Класс судна
	//4 Пустые: Проект, судно, класс судна
	Запрос.Текст = "ВЫБРАТЬ
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВГрузу КАК ВремяВГрузу,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВБалласте КАК ВремяВБалласте,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.Расстояние КАК Расстояние
	|ИЗ
	|	РегистрСведений.рарусАктуальныеНормыСледованияПоПроектамСудов.СрезПоследних(
	|			&Дата,
	|			ПроектСудна = &ПроектСудна
	|				И ПунктОтправления = &ПредыдущийПункт
	|				И ПунктСледования = &ТекущийПункт) КАК рарусАктуальныеНормыСледованияПоПроектамСудов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВГрузу,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВБалласте,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.Расстояние
	|ИЗ
	|	РегистрСведений.рарусАктуальныеНормыСледованияПоПроектамСудов.СрезПоследних(
	|			&Дата,
	|			Судно = &ОсновноеСудно
	|				И ПунктОтправления = &ПредыдущийПункт
	|				И ПунктСледования = &ТекущийПункт) КАК рарусАктуальныеНормыСледованияПоПроектамСудов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВГрузу,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВБалласте,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.Расстояние
	|ИЗ
	|	РегистрСведений.рарусАктуальныеНормыСледованияПоПроектамСудов.СрезПоследних(
	|			&Дата,
	|			КлассСудна = &КлассСудна
	|				И ПунктОтправления = &ПредыдущийПункт
	|				И ПунктСледования = &ТекущийПункт) КАК рарусАктуальныеНормыСледованияПоПроектамСудов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВГрузу,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.ВремяВБалласте,
	|	рарусАктуальныеНормыСледованияПоПроектамСудов.Расстояние
	|ИЗ
	|	РегистрСведений.рарусАктуальныеНормыСледованияПоПроектамСудов.СрезПоследних(
	|			&Дата,
	|			Судно = &ПустоеСудно
	|				И ПроектСудна В (&ПустойПроект)
	|				И КлассСудна = &ПустойКлассСудна
	|				И ПунктОтправления = &ПредыдущийПункт
	|				И ПунктСледования = &ТекущийПункт) КАК рарусАктуальныеНормыСледованияПоПроектамСудов";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ГрузБалласт Тогда
			СтруктураВозврата.Вставить("Время", Выборка.ВремяВГрузу*60);
		Иначе
			СтруктураВозврата.Вставить("Время", Выборка.ВремяВБалласте*60);
		КонецЕсли; 
		СтруктураВозврата.Вставить("Расстояние", Выборка.Расстояние);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
КонецФункции
// -- rarus Камаев П.В. 03.04.2020
