#Если Сервер Или ВнешнееСоединение Или ТолстыйКлиентОбычноеПриложение Тогда
	
Функция СтруктураПоследнегоИндекса() 
	
	СтруктураОтвета = Новый Структура("Индекс, ДатаУстановки", 0, Дата("00010101"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	рарусИндексКодаСинхронизации.Индекс КАК Индекс,
	               |	рарусИндексКодаСинхронизации.ДатаУстановки КАК ДатаУстановки
	               |ИЗ
	               |	РегистрСведений.рарусИндексКодаСинхронизации КАК рарусИндексКодаСинхронизации";
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураОтвета, Результат)
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

Функция СтруктураНовогоИндекса() 
	
	СтруктураИндекса = СтруктураПоследнегоИндекса();
	Дата = ТекущаяДатаСеанса();
	НаборЗаписей = РегистрыСведений.рарусИндексКодаСинхронизации.СоздатьНаборЗаписей();
	НоваяЗапись = НаборЗаписей.Добавить();
	
	Если НачалоГода(СтруктураИндекса.ДатаУстановки) = НачалоГода(Дата) Тогда
		НовыйИндекс = СтруктураИндекса.Индекс + 1
	Иначе
		НовыйИндекс = 1
	КонецЕсли;
	
	НоваяЗапись.Индекс = НовыйИндекс;
	НоваяЗапись.ДатаУстановки = Дата;
	НаборЗаписей.Записать(Истина);
	
	Возврат Новый Структура("Индекс, ДатаУстановки", НовыйИндекс, Дата);
	
КонецФункции

Функция НовыйКодСинхронизации(Судно = Неопределено) Экспорт
	
	СтруктураИндекса = СтруктураНовогоИндекса();
	ПрефиксИБ = ОбменДаннымиСервер.ПрефиксИнформационнойБазы();
	КодСинхронизации = СтрШаблон("%1-%2-%3.%4", ПрефиксИБ,
											?(Судно = Неопределено ИЛИ Судно = ПредопределенноеЗначение("Справочник.vftСуда.ПустаяСсылка"), "", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Судно, "КодБыстрогоВвода")),
											СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Строка(СтруктураИндекса.Индекс), 10, "0", "Слева"),
											Строка(Формат(СтруктураИндекса.ДатаУстановки, "ДФ=ddMMyyyy")));
	Возврат КодСинхронизации;
	
КонецФункции

#КонецЕсли