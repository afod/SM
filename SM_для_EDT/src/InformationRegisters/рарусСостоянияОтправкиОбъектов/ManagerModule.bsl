#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Проверяет, нужно ли выводить подключаемую команду "Зарегистрировать и отправить"
// Параметры:
// 	Источники - ДеревоЗначений, Ссылка - сведения о поставщиках команд этой формы. См. описание параметра процедуры ПриОпределенииКомандПодключенныхКОбъекту
//								в модуле ПодключаемыеКомандыПереопределяемый
Функция РегистрироватьСтатусОбъекта(Источники) Экспорт
	
	Если ОбменДаннымиСервер.ЭтоПодчиненныйУзелРИБ()
		И рарусИмущественныйУчетСервер.ИспользоватьИмущественныйУчет() Тогда
		ТипВхПараметра = ТипЗнч(Источники);
		Если ТипВхПараметра = Тип("ДеревоЗначений") Тогда
			Если Источники.Строки.Количество() Тогда
				МетаданныеИсточника = Источники.Строки[0].Метаданные;
				ТипИсточника = Тип(СтрШаблон("%1Ссылка.%2", Источники.Строки[0].Вид, МетаданныеИсточника.Имя));
				Возврат Метаданные.ОпределяемыеТипы.рарусДокументыСКонтролемСостоянияОтправки.Тип.СодержитТип(ТипИсточника);
			Иначе
				Возврат Ложь;
			КонецЕсли;
		ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипВхПараметра) Тогда
			Возврат Метаданные.ОпределяемыеТипы.рарусДокументыСКонтролемСостоянияОтправки.Тип.СодержитТип(ТипВхПараметра);
		Иначе
			Возврат Ложь
		КонецЕсли;
	Иначе 
		Возврат Ложь
	КонецЕсли;
		
КонецФункции		

Процедура ДобавитьПодключаемуюКоманду(Команды, РежимЗаписи = "Проводить")
	
	КомандаОтправки = Команды.Добавить();
	КомандаОтправки.Вид = "ОтправкаНаБерег";
	КомандаОтправки.Важность = "Важное";
	КомандаОтправки.Назначение = "ДляОбъекта";
	КомандаОтправки.МножественныйВыбор = Ложь;
	КомандаОтправки.Менеджер = "РегистрСведений.рарусСостоянияОтправкиОбъектов";
	КомандаОтправки.Обработчик = "ВыполнитьКомандуОтправкиОбъекта";
	//++ rarus makole 2021-04-21 [Задача № 28685]
	//КомандаОтправки.Представление = "Зарегистрировать к отправке в береговую систему";
	//КомандаОтправки.ТолькоВоВсехДействиях = Ложь;
	КомандаОтправки.Представление = "Отправить в береговую систему";
	КомандаОтправки.ТолькоВоВсехДействиях = Истина;
	//-- rarus makole 2021-04-21 [Задача № 28685]
	КомандаОтправки.ИмяФормы = "Форма.ФормаКоманды";
	КомандаОтправки.ИмяПараметраФормы = "СсылкаНаОбъект";
	КомандаОтправки.РежимЗаписи = РежимЗаписи;
	
КонецПроцедуры

Процедура ВыполнитьКомандуОтправкиОбъекта(СсылкаНаИсточник) Экспорт
	УстановитьСтатусОбъекта(СсылкаНаИсточник, ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.ЗарегистрированКОтправке"));
КонецПроцедуры

Процедура УстановитьСтатусОбъекта(СсылкаНаОбъект, Статус) Экспорт
	
	УстановитьПривилегированныйРежим(Истина); // ++ rarus makole 2021-06-24
	
	СостояниеОбъекта = РегистрыСведений.рарусСостоянияОтправкиОбъектов.СоздатьМенеджерЗаписи();
	СостояниеОбъекта.КонтролируемыйОбъект = СсылкаНаОбъект;
	СостояниеОбъекта.СостояниеОтправки = Статус;
	СостояниеОбъекта.Записать();
	
	УстановитьПривилегированныйРежим(Ложь); // ++ rarus makole 2021-06-24
	
КонецПроцедуры

Функция ОбъектДоступенДляРедактирования(СсылкаНаОбъект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	рарусСостоянияОтправкиОбъектов.СостояниеОтправки КАК СостояниеОтправки
		|ИЗ
		|	РегистрСведений.рарусСостоянияОтправкиОбъектов КАК рарусСостоянияОтправкиОбъектов
		|ГДЕ
		|	рарусСостоянияОтправкиОбъектов.КонтролируемыйОбъект = &СсылкаНаОбъект";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	
	ВыборкаСостояния = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаСостояния.Следующий() Тогда
		Если ВыборкаСостояния.СостояниеОтправки = ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.ЗарегистрированКОтправке")
			ИЛИ ВыборкаСостояния.СостояниеОтправки = ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.Черновик") Тогда
			Возврат Истина
		Иначе
			Возврат Ложь
		КонецЕсли;
	Иначе
		Возврат Ложь
	КонецЕсли;
		
КонецФункции

Функция ТекущийСтатусОбъекта(СсылкаНаОбъект) Экспорт
	
	Если СсылкаНаОбъект.Пустая() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.Черновик")
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	рарусСостоянияОтправкиОбъектов.СостояниеОтправки КАК СостояниеОтправки
		|ИЗ
		|	РегистрСведений.рарусСостоянияОтправкиОбъектов КАК рарусСостоянияОтправкиОбъектов
		|ГДЕ
		|	рарусСостоянияОтправкиОбъектов.КонтролируемыйОбъект = &СсылкаНаОбъект";
	запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	ВыборкаСтатус = Запрос.Выполнить().Выбрать();
	Если ВыборкаСтатус.Следующий() Тогда
		Возврат ВыборкаСтатус.СостояниеОтправки
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.Отправлен")
	КонецЕсли;
	
КонецФункции

Процедура ОбработатьСтатусыОтправленныхДокументов() Экспорт
	
	УстановитьПривилегированныйРежим(Истина); // ++ rarus makole 2021-09-16
	
	Если ОбменДаннымиСервер.ЭтоПодчиненныйУзелРИБ() Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	рарусСостоянияОтправкиОбъектов.КонтролируемыйОбъект КАК СсылкаНаОбъект
			|ИЗ
			|	РегистрСведений.рарусСостоянияОтправкиОбъектов КАК рарусСостоянияОтправкиОбъектов
			|ГДЕ
			|	рарусСостоянияОтправкиОбъектов.СостояниеОтправки = ЗНАЧЕНИЕ(Перечисление.рарусСостояниеОтправкиОбъекта.ЗарегистрированКОтправке)";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаКОбработке = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаКОбработке.Следующий() Цикл
			Если Не рарусСостоянияРегистрацииОбъектовНаПланахОбменаСервер.ПланОбменаПолныйОбъектДоступенДляРедактирования(ВыборкаКОбработке.СсылкаНаОбъект) Тогда
				УстановитьСтатусОбъекта(ВыборкаКОбработке.СсылкаНаОбъект, ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.Отправлен"))
			КонецЕсли;
		КонецЦикла;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	рарусСостоянияОтправкиОбъектов.КонтролируемыйОбъект КАК СсылкаНаОбъект
			|ИЗ
			|	РегистрСведений.рарусСостоянияОтправкиОбъектов КАК рарусСостоянияОтправкиОбъектов
			|ГДЕ
			|	рарусСостоянияОтправкиОбъектов.СостояниеОтправки = ЗНАЧЕНИЕ(Перечисление.рарусСостояниеОтправкиОбъекта.ЗагруженИзБМ)";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаКОбработке = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаКОбработке.Следующий() Цикл
			УстановитьСтатусОбъекта(ВыборкаКОбработке.СсылкаНаОбъект, ПредопределенноеЗначение("Перечисление.рарусСостояниеОтправкиОбъекта.Отправлен"))
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь); // ++ rarus makole 2021-09-16
	
КонецПроцедуры

// ++ rarus makole 2021-09-14 [РАIT-0023495]
// Доработка отдельных механизмов по учёту и списанию ТМЦ на судах
Процедура ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, Источники, ПодключенныеОтчетыИОбработки, Команды) Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.рарусСостоянияОтправкиОбъектов)
		И РегистрироватьСтатусОбъекта(Источники) Тогда
		
		Если СтрНайти(НастройкиФормы.ИмяФормы, "Документ.рарусИнвентаризацияТМЦ") Тогда
			РежимЗаписи = "Записывать"
		Иначе
			РежимЗаписи = "Проводить"
		КонецЕсли;
		ДобавитьПодключаемуюКоманду(Команды, РежимЗаписи);
		
	КонецЕсли;
	
КонецПроцедуры
// -- rarus makole 2021-09-14 [РАIT-0023495]

#КонецЕсли