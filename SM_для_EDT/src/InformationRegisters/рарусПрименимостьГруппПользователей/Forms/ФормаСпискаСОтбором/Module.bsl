
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗапросПоПрименимости = Новый Запрос;
	ЗапросПоПрименимости.Текст = "ВЫБРАТЬ
		|	ЕСТЬNULL(рарусПрименимостьГруппПользователей.ГруппаПользователей, &ГруппаПользователей) КАК ГруппаПользователей,
		|	vftСуда.Ссылка КАК Судно,
		|	ЕСТЬNULL(рарусПрименимостьГруппПользователей.Используется, ЛОЖЬ) КАК Используется
		|ИЗ
		|	Справочник.vftСуда КАК vftСуда
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.рарусПрименимостьГруппПользователей КАК рарусПрименимостьГруппПользователей
		|		ПО рарусПрименимостьГруппПользователей.Судно = vftСуда.Ссылка
		|			И (рарусПрименимостьГруппПользователей.ГруппаПользователей = &ГруппаПользователей)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Используется УБЫВ,
		|	Судно
		|АВТОУПОРЯДОЧИВАНИЕ";
	ЗапросПоПрименимости.УстановитьПараметр("ГруппаПользователей", Параметры.Отбор.ГруппаПользователей);
	
	ТаблицаРезультата = ЗапросПоПрименимости.Выполнить().Выгрузить();
	СписокЗаписей.Загрузить(ТаблицаРезультата)
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокЗаписейИспользуетсяПриИзмененииНаСервере(ДанныеСтроки)
	НаборЗаписей = РегистрыСведений.рарусПрименимостьГруппПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаПользователей.Установить(ДанныеСтроки.ГруппаПользователей);
	НаборЗаписей.Отбор.Судно.Установить(ДанныеСтроки.Судно);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеСтроки);
	НаборЗаписей.Записать(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаписейИспользуетсяПриИзменении(Элемент)
	ДанныеСтроки = Новый Структура("ГруппаПользователей, Судно, Используется");
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, Элементы.СписокЗаписей.ТекущиеДанные);
	СписокЗаписейИспользуетсяПриИзмененииНаСервере(ДанныеСтроки);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьНаВсе(ГруппаПользователей, Суда, Используется)
	НаборЗаписей = РегистрыСведений.рарусПрименимостьГруппПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаПользователей.Установить(ГруппаПользователей);
	Для каждого Судно Из Суда Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ГруппаПользователей = ГруппаПользователей;
		НоваяЗапись.Судно = Судно;
		НоваяЗапись.Используется = Используется;
	КонецЦикла;
	НаборЗаписей.Записать(Истина);
КонецПроцедуры

&НаСервере
Функция МассивСудов()
	Возврат СписокЗаписей.Выгрузить(,"Судно").ВыгрузитьКолонку("Судно");
КонецФункции

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Суда = МассивСудов();
	ДопПараметры = Новый Структура("Суда, ГруппаПользователей, Используется", 
								Суда,
								Элементы.СписокЗаписей.ТекущиеДанные.ГруппаПользователей,
								Истина);
	ОповещениеОПродолжении = Новый ОписаниеОповещения("ПродолжитьОбработкуУстановкиПрименимости", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(ОповещениеОПродолжении, "Установить применимость группы пользователей для всех судов?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, "Внимание!");
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Суда = МассивСудов();
	ДопПараметры = Новый Структура("Суда, ГруппаПользователей, Используется", 
								Суда,
								Элементы.СписокЗаписей.ТекущиеДанные.ГруппаПользователей,
								Ложь);
	ОповещениеОПродолжении = Новый ОписаниеОповещения("ПродолжитьОбработкуУстановкиПрименимости", ЭтаФорма, ДопПараметры);
	ПоказатьВопрос(ОповещениеОПродолжении, "Снять применимость группы пользователей для всех судов?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, "Внимание!");
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОбработкуУстановкиПрименимости(Ответ, ДопПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УстановитьНаВсе(ДопПараметры.ГруппаПользователей, ДопПараметры.Суда, ДопПараметры.Используется);
		Для каждого СтрокаЗаписи Из СписокЗаписей Цикл
			СтрокаЗаписи.Используется = ДопПараметры.Используется
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры