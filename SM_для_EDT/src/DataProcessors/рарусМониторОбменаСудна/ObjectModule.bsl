Процедура ОбработатьСлужебныеДанные(АдресДанныхСБерега) Экспорт
	
	ДатаНачалаВыгрузки = ТекущаяДатаСеанса();
	
	Результат = ПолучитьИзВременногоХранилища(АдресДанныхСБерега);
	
	Если Результат.Свойство("ns") Тогда // Есть изменение настроек обмена, загрузим
		
		ДатаНачалаЗагрузки = ТекущаяДатаСеанса();
		рарусСинхронизацияССудном.ОбработатьМассивЗагруженныхОбъектов(Результат.ns, ГлавныйУзел);
		
		МассивНомеровОтЦентра = СтрРазделить(Результат.io,";",Истина);
		
		// ++ rarus makole 2021-06-29
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		
		Попытка
			рарусСинхронизацияССудном.ЗаблокироватьУзел(ГлавныйУзел);
			ОбъектУзла = ГлавныйУзел.ПолучитьОбъект();
			ОбъектУзла.НомерПринятого = Число(МассивНомеровОтЦентра[1]);
			ОбъектУзла.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Блокировка узла плана обмена'"), УровеньЖурналаРегистрации.Ошибка, , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			// ++ rarus makole 2021-06-29
			рарусСинхронизацияССудном.ЗафиксироватьРезультатыОбменаВРегистрах(ГлавныйУзел,
																		Перечисления.ДействияПриОбмене.ЗагрузкаДанных,
																		Перечисления.РезультатыВыполненияОбмена.Ошибка,
																		ДатаНачалаЗагрузки,
																		ТекущаяДатаСеанса(),
																		Число(МассивНомеровОтЦентра[1]),
																		ИмяКомпьютера());
			// -- rarus makole 2021-06-29
	      	ВызватьИсключение("Не удалось установить новый номер полученного сообщения в узле судна, повторите попытку позднее");
		КонецПопытки;
		// -- rarus makole 2021-06-29
	
		// ++ rarus makole 2021-06-29
		рарусСинхронизацияССудном.ЗафиксироватьРезультатыОбменаВРегистрах(ГлавныйУзел,
																	Перечисления.ДействияПриОбмене.ЗагрузкаДанных,
																	Перечисления.РезультатыВыполненияОбмена.Выполнено,
																	ДатаНачалаЗагрузки,
																	ТекущаяДатаСеанса(),
																	Число(МассивНомеровОтЦентра[1]),
																	ИмяКомпьютера());
		// -- rarus makole 2021-06-29
																	
	КонецЕсли;
	
	// ++ rarus makole 2021-03-26 [Задача № 27965]
	ВерсияКонфигурацииЦБ = Результат.ver;
	// -- rarus makole 2021-03-26 [Задача № 27965]
	Обработки.рарусМониторОбменаСудна.ЗаполнитьТаблицуПодготовленных(ПодготовленоКЗагрузке, Результат.oo);
	СохранитьИнформациюОПодготовленныхКЗагрузке(ГлавныйУзел, ВариантОбмена, ПодготовленоКЗагрузке);
	
КонецПроцедуры

Процедура СохранитьИнформациюОПодготовленныхКЗагрузке(Узел, Вариант, Знач ТЧДанныеКСохранению)
	
	ДанныеКСохранению = ТЧДанныеКСохранению.Выгрузить();
	НаборЗаписей = РегистрыСведений.рарусОбъектыПодготовленныеКЗагрузкеВГлавномУзле.СоздатьНаборЗаписей(); 

	НаборЗаписей.Отбор.Узел.Установить(Узел); 
	НаборЗаписей.Отбор.Вариант.Установить(Вариант); 

	Для каждого СтрокаКСохранению Из ДанныеКСохранению Цикл
		НоваяЗапись = НаборЗаписей.Добавить(); 
		НоваяЗапись.Узел = Узел; 
		НоваяЗапись.Вариант = Вариант; 
		НоваяЗапись.ВидОбъекта = СтрокаКСохранению.ВидОбъекта; 
		НоваяЗапись.Количество = СтрокаКСохранению.Количество;
		НоваяЗапись.Размер = СтрокаКСохранению.Объем;
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

