
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьВсеНастройки(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОчиститьВсеНастройки(Команда)
	
	ПользовательСсылка = ПользователиКлиент.ТекущийПользователь();
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Очистить все настройки у пользователя ""%1""?
		|Отменить результат этого действия будет нельзя.'"), Строка(ПользовательСсылка));
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("Очистить", НСтр("ru = 'Очистить'"));
	КнопкиВопроса.Добавить("Отмена",   НСтр("ru = 'Отмена'"));
	
	Оповещение = Новый ОписаниеОповещения("ОчиститьВсеНастройкиЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, КнопкиВопроса,, КнопкиВопроса[1].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеНастройкиЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = "Отмена" Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательСсылка = ПользователиКлиент.ТекущийПользователь();
	
	ОчищаемыеНастройки = Новый Массив;
	ОчищаемыеНастройки.Добавить("НастройкиОтчетов");
	ОчищаемыеНастройки.Добавить("НастройкиВнешнегоВида");
	ОчищаемыеНастройки.Добавить("ДанныеФорм");
	ОчищаемыеНастройки.Добавить("ПерсональныеНастройки");
	ОчищаемыеНастройки.Добавить("Избранное");
	ОчищаемыеНастройки.Добавить("НастройкиПечати");
	ОчищаемыеНастройки.Добавить("ПрочиеПользовательскиеНастройки");
	
	ОчиститьВсеНастройкиНаСервере(ОчищаемыеНастройки);
	// ++ rarus yukuzi 22.10.2021   //
	vftОбщегоНазначенияВызовСервера.РазместитьИнфопанельНаНачальнойСтранице();
	// -- rarus yukuzi 22.10.2021
	ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	
	ТекстПояснения = НСтр("ru = 'Очищены все настройки пользователя ""%1""'");
	ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПояснения, ПользовательСсылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Очистка настроек'"), , ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьВсеНастройкиНаСервере(ОчищаемыеНастройки)
	
	ПользовательСсылка = Пользователи.ТекущийПользователь();
	
	МассивПользователей = Новый Массив;
	МассивПользователей.Добавить(ПользовательСсылка);
	
	Обработки.НастройкиПользователей.УдалитьНастройкиПользователей(ОчищаемыеНастройки, МассивПользователей);
	
КонецПроцедуры

#КонецОбласти