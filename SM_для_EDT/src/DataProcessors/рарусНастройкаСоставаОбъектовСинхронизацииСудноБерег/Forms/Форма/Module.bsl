&НаСервере
Процедура ЗаполнитьПоУмолчаниюНаСервере()
	
	Если Не ЗначениеЗаполнено(Вариант) Тогда
		Возврат
	КонецЕсли;
	
	Объект.Настройки.Очистить();
	
	ТаблицаНастроек = РегистрыСведений.рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.ПолучитьНастройкиПоУмолчанию(Вариант);
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаНастроек.Объект КАК Объект,
		|	ТаблицаНастроек.[Выгружать] КАК Выгружать,
		|	ТаблицаНастроек.[Загружать] КАК Загружать
		|ПОМЕСТИТЬ ВТ_ТаблицаНастроек
		|ИЗ
		|	&ТаблицаНастроек КАК ТаблицаНастроек
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаНастроек.Объект КАК Объект,
		|	ВТ_ТаблицаНастроек.Выгружать КАК Выгружать,
		|	ВТ_ТаблицаНастроек.Загружать КАК Загружать,
		|	ИдентификаторыОбъектовМетаданных.Родитель КАК ВидОбъекта,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Документы""
		|			ТОГДА 1
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Справочники""
		|			ТОГДА 2
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Регистры сведений""
		|			ТОГДА 3
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Регистры накопления""
		|			ТОГДА 4
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Константы""
		|			ТОГДА 5
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Задачи""
		|			ТОГДА 6
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Бизнес-процессы""
		|			ТОГДА 7
		|		ИНАЧЕ 7
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	ВТ_ТаблицаНастроек КАК ВТ_ТаблицаНастроек
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО ВТ_ТаблицаНастроек.Объект = ИдентификаторыОбъектовМетаданных.Ссылка
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	ИдентификаторыОбъектовМетаданных.Синоним
		|ИТОГИ ПО
		|	ВидОбъекта";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[Выгружать]", "Выгружать" + Вариант);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[Загружать]", "Загружать" + Вариант);
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТаблицаНастроек", ТаблицаНастроек);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаВидОбъекта = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаВидОбъекта.Следующий() Цикл
		
		СтрокаВида = Объект.Настройки.Добавить();
		СтрокаВида.Объект = ВыборкаВидОбъекта.ВидОбъекта;
		СтрокаВида.ЭтоВид = Истина;
	
		ВыборкаДетальныеЗаписи = ВыборкаВидОбъекта.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СтрокаОбъекта = Объект.Настройки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОбъекта, ВыборкаДетальныеЗаписи);
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Вариант) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран вариант настроек",,"Вариант");
		Возврат;
	Иначе
		ЗаполнитьПоУмолчаниюНаСервере()
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиСохраненнымиДанными()
	
	Объект.Настройки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.Объект КАК Объект,
		|	рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.Выгружать КАК Выгружать,
		|	рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.Загружать КАК Загружать,
		|	ИдентификаторыОбъектовМетаданных.Родитель КАК ВидОбъекта,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Документы""
		|			ТОГДА 1
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Справочники""
		|			ТОГДА 2
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Регистры сведений""
		|			ТОГДА 3
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Регистры накопления""
		|			ТОГДА 4
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Константы""
		|			ТОГДА 5
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Задачи""
		|			ТОГДА 6
		|		КОГДА ИдентификаторыОбъектовМетаданных.Родитель.Синоним = ""Бизнес-процессы""
		|			ТОГДА 7
		|		ИНАЧЕ 7
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	РегистрСведений.рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег КАК рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
		|		ПО рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.Объект = ИдентификаторыОбъектовМетаданных.Ссылка
		|ГДЕ
		|	рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.КодУзла = &КодУзла
		|	И рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.Вариант = &Вариант
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	ИдентификаторыОбъектовМетаданных.Синоним
		|ИТОГИ ПО
		|	ВидОбъекта";
	
	Запрос.УстановитьПараметр("Вариант", Вариант);
	Запрос.УстановитьПараметр("КодУзла", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Узел, "Код"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаВидОбъекта = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаВидОбъекта.Следующий() Цикл
		
		СтрокаВида = Объект.Настройки.Добавить();
		СтрокаВида.Объект = ВыборкаВидОбъекта.ВидОбъекта;
		СтрокаВида.ЭтоВид = Истина;
	
		ВыборкаДетальныеЗаписи = ВыборкаВидОбъекта.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СтрокаОбъекта = Объект.Настройки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОбъекта, ВыборкаДетальныеЗаписи);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УзелПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Вариант) И 
		ЗначениеЗаполнено(Объект.Узел) Тогда
		
		ЗаполнитьНастройкиСохраненнымиДанными();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Узел) Тогда
		
		СнятьВсе(Неопределено);
		
		МассивСоСтрокойВыбранногоУзла = Объект.Узлы.НайтиСтроки(Новый Структура("Узел", Объект.Узел));
		МассивСоСтрокойВыбранногоУзла[0].Устанавливать = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВариантПриИзменении(Элемент)
	
	Если Объект.Вариант = "Срочное (важное)" Тогда
		Вариант = 1
	ИначеЕсли Объект.Вариант = "Стандартный обмен" Тогда
		Вариант = 2
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Узел) Тогда
		ЗаполнитьНастройкиСохраненнымиДанными()
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НастройкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Полный.Ссылка КАК Узел,
		|	Полный.Код КАК КодУзла
		|ИЗ
		|	ПланОбмена.Полный КАК Полный
		|ГДЕ
		|	Полный.ЭтотУзел = ЛОЖЬ
		|	И Полный.ПометкаУдаления = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Узел
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	ТаблицаУзлов = Запрос.Выполнить().Выгрузить();
	Объект.Узлы.Загрузить(ТаблицаУзлов);

КонецПроцедуры

&НаКлиенте
Процедура УзлыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УзлыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Для каждого СтрокаУзла Из Объект.Узлы Цикл
		СтрокаУзла.Устанавливать = Истина
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для каждого СтрокаУзла Из Объект.Узлы Цикл
		СтрокаУзла.Устанавливать = Ложь
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДляУзлов(Команда)
	
	ОповещениеОЗаписи = Новый ОписаниеОповещения("ОбработатьОповещениеОЗаписи", ЭтаФорма);
	
	ПоказатьВопрос(ОповещениеОЗаписи, "Для выбранных узлов будут заменены настройки синхронизации. Продолжить?", РежимДиалогаВопрос.ДаНет,,,"Внимание!");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещениеОЗаписи(Ответ,ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СохранитьНастройкиНаСервере()
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	СписокУзлов = Объект.Узлы.НайтиСтроки(Новый Структура("Устанавливать", Истина));
	СтрокиКЗаписи = Объект.Настройки.НайтиСтроки(Новый Структура("ЭтоВид", Ложь));
	// makole доработать запись только изменённых настроек для уменьшения трафика
	Для каждого СтрокаУзла Из СписокУзлов Цикл
		
		Для каждого СтрокаНастройки Из СтрокиКЗаписи Цикл
			
			НастройкиНабор = РегистрыСведений.рарусНастройкиСоставаОбъектовСинхронизацииСудноБерег.СоздатьНаборЗаписей();
			НастройкиНабор.Отбор.КодУзла.Установить(СтрокаУзла.КодУзла);
			НастройкиНабор.Отбор.Вариант.Установить(Вариант);
			НастройкиНабор.Отбор.Объект.Установить(СтрокаНастройки.Объект);
			НастройкиНабор.Прочитать();
			
			Если НастройкиНабор.Количество() > 0 Тогда
				СтараяЗапись = НастройкиНабор[0];
				Если СтараяЗапись.Выгружать = СтрокаНастройки.Выгружать
					И СтараяЗапись.Загружать = СтрокаНастройки.Загружать Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			НастройкиНабор.Очистить();

			НоваяЗапись = НастройкиНабор.Добавить();
			НоваяЗапись.КодУзла 	= СтрокаУзла.КодУзла;
			НоваяЗапись.Вариант 	= Вариант;
			НоваяЗапись.Объект 		= СтрокаНастройки.Объект;
			НоваяЗапись.Выгружать 	= СтрокаНастройки.Выгружать;
			НоваяЗапись.Загружать 	= СтрокаНастройки.Загружать;
			НастройкиНабор.Записать();
			
		КонецЦикла;
		
		
	КонецЦикла;
	
КонецПроцедуры
